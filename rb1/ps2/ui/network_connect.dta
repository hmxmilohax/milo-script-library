; We use this function to go to screens that require a connectivity test
; on the PS3.  $dest is the destination screen.  If the check is strict, we
; don't goto_screen if the connection check fails.  If it's not strict, we just
; alert the user that it didn't work.
{func goto_after_network_connect
   ($dest $strict)

   #ifdef HX_PS3
   {if_else {&& {! {platform_mgr is_ethernet_cable_connected}} {! $strict}}
      ; If the user has no ethernet cable connected, we don't bother him about a connection
      {ui goto_screen $dest}
      {if_else {platform_mgr has_online_privilege -1}
         {do
            ; attempt to connect to the network
            {network_start_panel set destination $dest}
            {network_start_panel set strict $strict}
            {ui push_screen network_start_screen}
         }
         {do
            ; the user is not permitted online
            {no_online_privilege_screen set destination $dest}
            {no_online_privilege_screen set strict $strict}
            {ui push_screen no_online_privilege_screen}
         }
      }
   }
   #else
   {ui goto_screen $dest}
   #endif
}

; Cancels the goto_after_network_connect
{func cancel_network_goto
   {gamemode set_mode init} ; the mode was set before attempting the goto
   {session clear} ; in case we added a player to the session (solo modes)
   {ui pop_screen}
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; PS3 net start dialog screen

{new UIPanel
   network_start_panel
   (destination '')
   (strict FALSE)

   (TRANSITION_COMPLETE_MSG
      {platform_mgr add_sink $this}
      {platform_mgr run_net_start_utility}
   )

   (net_start_utility_finished
      ($connected)
      {platform_mgr remove_sink $this}

      {if_else $connected
         ; The utility succeeded, we are connected
         {if_else {&& [strict] {! {server is_connected}}}
            {do ; now we must login to Rock Central before proceeding
               {server_connect_panel set destination [destination]}
               {ui goto_screen server_connect_screen}
            }
            {ui pop_screen [destination]} ; we're connected and free to go
         }
         ; The utility failed, we can't connect to Rock Central
         {if_else [strict]
            {cancel_network_goto} ; We can't proceed
            {do
               ; Allow the user to proceed but display a dialog first
               {network_warning_screen set destination [destination]}
               {ui goto_screen network_warning_screen}
            }
         }
      }
   )
}

{new UIScreen
   network_start_screen
   (panels network_start_panel)
   (focus network_start_panel)
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Server Connection screen

{new UIPanel
   server_connect_panel
   (file server_message.milo)

   (destination '')

   (TRANSITION_COMPLETE_MSG {set $waiting_for_server TRUE})

   (cancel_goto
      {set $waiting_for_server FALSE}
      {cancel_network_goto}
   )

   (poll
      {if $waiting_for_server
         {unless {platform_mgr is_connected}
            ; we have no internet
            {$this cancel_goto}
         }
         {switch {rock_central state}
            (kRCOnline
               {set $waiting_for_server FALSE}
               {ui pop_screen [destination]}
            )
            (kRCFailed
               {$this cancel_goto} ; our login attempt failed
            )
         }
      }
   )
}

{new UIScreen
   server_connect_screen
   (panels server_connect_panel)
   (focus server_connect_panel)
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Network Warning screen

{new UIScreen
   network_warning_screen
   (panels dialog_panel)
   (focus dialog_panel)

   (destination '')

   (enter
      {dialog_panel set_yesno
         {localize_token {gamemode get cannot_connect_warning}}
         yes.btn
      }
   )

   (SELECT_START_MSG
      {switch $component
         (yes.btn {ui pop_screen [destination]})
         (no.btn {cancel_network_goto})
      }
      kDataUnhandled
   )
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; No Online Privilege screen
{new UIScreen
   no_online_privilege_screen
   (panels dialog_panel)
   (focus dialog_panel)

   (destination '')
   (strict FALSE)

   (enter
      {if_else [strict]
         {dialog_panel set_ok {localize_token privileges_restricted_strict}}
         {dialog_panel set_ok {localize_token privileges_restricted_lenient}}
      }
   )

   (SELECT_START_MSG
      {if_else [strict]
         {cancel_network_goto}
         {ui pop_screen [destination]}
      }
      kDataUnhandled
   )
}