; solo tour character select ("matchmaking" screen)
{new UIPanel tour_solo_char_panel
   (file "tour_solo_char.milo")
   (focus char.lst)
   (reset_focus TRUE)
   LIMIT_TO_LEADER_SESSION
   (char_provider {object ""})
      
   (enter
      ; now we care about controllers
      {band_ui_sink set post_matchmaking TRUE}

      ; the CharSelectPanel wants to know when to show/hide chars
      {practice_space_panel set_showing_callback $this}
      
      {do ($pcfg {game get_player_config {game get leader_num}})

         ;; make sure there's no character set yet
         ;; (it'll muck up the character list)
         {$pcfg set_char 0}

         {gamertag.lbl set_localized {$pcfg get player_name}}
         {instrument.lbl set_localized 
            {get_font_char_from_controller_type {$pcfg get_controller_type}}
         }
         
         {set [char_provider] {get_char_provider {game get leader_num}}}
         {[char_provider] set_instrument_filter {$pcfg get_controller_sym}}
         {char.lst set_provider [char_provider]}
      
         ; try and keep current char selected
         {do ($char_pos {[char_provider] get_pos_from_char {$pcfg get_char}})
            {unless {== $char_pos -1}
               {char.lst set_selected $char_pos}   
            }
         }
         {band_ui_sink enable_lefty_override {$pcfg get player_num} TRUE}
      }
      {$this set_gender FALSE}
      {$this update}
   )
   
   (exit
      {do ($pcfg {game get_player_config {game get leader_num}})
         {band_ui_sink enable_lefty_override {$pcfg get player_num} FALSE}
      }
   )

   (unload
      {practice_space_panel set_showing_callback ''}
      ; free any new characters we have created
      {charcreator_panel free_all}
   )
   
   (update
      {do ($pos {char.lst selected_pos})
          ($pcfg {game get_player_config {game get leader_num}})
          ($char 
            {if_else {[char_provider] is_create_new $pos}
               0
               {[char_provider] get_char $pos}
            }
          )
         {$pcfg set_char $char}
      }
   )

   (SCROLL_MSG
      {$this update}
   )

   (FOCUS_MSG
      {if_else {|| {== male.btn $new_focus} {== female.btn $new_focus}}
         ; start loading up a new character for this player
         {charcreator_panel request
            {game get leader_num} 
            {basename {$new_focus name}}
         }
         ; don't keep the new guy around
         {charcreator_panel free {game get leader_num}}
      }
   )

   (SELECT_MSG
      {if_else {== $component char.lst}
         {if_else {[char_provider] is_create_new {$component selected_pos}}
            {if_else
               {profile_mgr can_player_create_char
                  {game get leader_num}
                  {{game get_player_config {game get leader_num}} get_controller_sym}
               }
               ; then
               {$this set_gender TRUE}
               ; else
               {ui push_screen tour_solo_char_no_room_screen}
            }
            ; else
            {do
               {tour_solo_hub_panel set reset TRUE} ; reset button
               {ui goto_screen {gamemode get post_matchmaking_screen}}
            }
         }
         {do
            ; create a new character for this player
            {charcreator_panel set char_player_num {game get leader_num}}
            {ui goto_screen charcreator_maker_screen}
         }
      }
   )

   (set_gender ($enable)
      {char.grp set_showing {! $enable}}
      {gender.grp set_showing $enable}
      {$this set_focus {if_else $enable male.btn char.lst}}
   )
   
   ; tells CharSelectPanel when to hide char
   (show_char ($player_num)
      {|| {!= {$this focus_name} char.lst}
          {! {[char_provider] is_create_new {char.lst selected_pos}}}
      }
   )
}

{new UIScreen tour_solo_char_screen
   (panels meta practice_space_panel tour_solo_char_panel tour_band_lower3rd_panel helpbar)
   (helpbar (helpbar_back helpbar_select))
   (focus tour_solo_char_panel)
   (back main_solomode_screen)
   
   (enter
      {practice_space_panel select_cam "solo_enter"}
   )
      
   (BUTTON_DOWN_MSG
      {if_else {&& {== $button kPad_Tri} {{tour_solo_char_panel find gender.grp} showing}}
         {tour_solo_char_panel set_gender FALSE}
         kDataUnhandled
      }   
   )
}

; out of character confirmation screen
{new UIScreen tour_solo_char_no_room_screen
   (panels dialog_panel)
   (focus dialog_panel)

   (enter
      {dialog_panel set_ok
         {sprintf {localize_token charcreator_no_more_chars_fmt}
            {profile_mgr get_max_chars_per_inst}
         }
      }
      {dialog_panel set limit session}
   )

   (SELECT_MSG
      {ui pop_screen}
   )
}

; solo tour difficulty select
{new UIPanel tour_solo_diff_panel
   (file "tour_solo_diff.milo")
   LIMIT_TO_LEADER_SESSION
   
   (enter
      {$this refresh}
   )
   
   (refresh
      {do ($pcfg {game get_player_config {game get leader_num}})
          ($char {$pcfg get_char})
         {$this set_focus 
            {sprintf "%s.btn" {{$pcfg get_char} solo_difficulty_sym}}
         }
         {foreach $diff (DIFF_SYMBOLS)
            {{sprintf "%s_progress.lbl" $diff} set_localized
               {fmt_songs_played
                  {$char world_fame $diff}
                  {$char total_world_fame $diff}
               }
            }
         }
      }
   )
   
   (SELECT_START_MSG
      {do ($pcfg {game get_player_config {game get leader_num}})
          ($diff {basename {$component name}})
         {$pcfg set_difficulty $diff}
         {{$pcfg get_char} set_solo_difficulty $diff}
      }

      ; reset the provider mode here
      {{tour solo_event_provider} set mode kTourVenueNormal}

      {ui goto_screen 
         {if_else {tour hint}
            {{tour hint} get screen}
            tour_solo_event_screen
         }
      }
      kDataUnhandled
   )
}

{new UIScreen tour_solo_diff_screen
   (panels meta practice_space_panel tour_solo_diff_panel tour_band_lower3rd_panel helpbar)
   (helpbar (helpbar_back helpbar_select))
   (focus tour_solo_diff_panel)
   (back tour_solo_hub_screen)   
}

; Solo Tour Hub
{new UIPanel tour_solo_hub_panel
   (file tour_solo_hub.milo)
   (reset TRUE)
   LIMIT_TO_LEADER_SESSION
      
   (enter
      {if [reset]
         {$this set_focus gigguide.btn}
      }
      {set [reset] FALSE}
      {$this refresh}
   )

   (TRANSITION_COMPLETE_MSG
      {autosave}
   )
   
   (refresh
      #ifdef TOUR_LOGGING
         {$log_tour print "MONEYBAL | "}
      #endif 
      {do ($char {{game get_player_config {game get leader_num}} get_char})
         ; set the TourChar to be the active TourPerformer
         {tour set_performer $char}
         
         {name.lbl  set_localized {$char get_char_name}}
         {part.lbl  set_localized {$char instrument_icon}}
         {money.lbl set_localized {fmt_money {$char money}}}
         #ifdef TOUR_LOGGING
            {$log_tour print {sprintf "%s:%d," {$char get_instrument} {$char money}}}
         #endif
         {tour_score.lbl set_localized
            {sprintf {localize tour_solo_tour_score}
               {fmt_score {$char tour_score}}
            }
         }
      }
      #ifdef TOUR_LOGGING
         {$log_tour print "\n"}
      #endif
   )
   
   (SELECT_START_MSG
      {switch $component
         (gigguide.btn
            {ui goto_screen tour_solo_diff_screen}
         )
         (rokkshoppe.btn
            ; restrict the shop to the player's controller and character
            {shop_char_panel set char_player_num $player_num}
            {shop_panel set reset_on_enter TRUE}
            {ui goto_screen shop_screen}
         )
         (playerprofile.btn
            {tour_char_profile_panel set player_num $player_num}
            {tour_char_profile_panel set profile_player_num $player_num}
            {ui goto_screen tour_solo_char_profile_screen}
         )
         (community.btn
            {ui goto_screen tour_hub_community_screen}
         )
      }
      kDataUnhandled
   )  
}

{new UIScreen tour_solo_hub_screen
   (panels 
      meta
      practice_space_panel
      tour_solo_hub_panel
      tour_band_lower3rd_panel
      helpbar
   )
   (helpbar (helpbar_back helpbar_select))
   (focus tour_solo_hub_panel)
   (back {gamemode get matchmaking_screen})

   (enter
      ;; set this here because it's a central point, and it's the return
      ;;   path from the rock shop
      {presence_mgr set_prepare_to_rock}
      
      {practice_space_panel select_cam solo_enter}
   )
}

; Character Profile
{new CharProfilePanel tour_char_profile_panel
   (file tour_char_profile.milo)
   (focus gamertag.btn)
   (player_num -1)
   (profile_player_num -1)

   (enter
      {$this set_char
         {{gamecfg get_player_config [profile_player_num]}
            get_char
         }
      }

      {$this disable topsongs.btn}

      {do
         ($is_local {$this is_local})
         ($op
            {if_else {&& $is_local {== [player_num] [profile_player_num]}}
               enable
               ; else
               disable
            }
         )

         {$this $op gamertag.btn}
         {$this $op motto.btn}

         {if_else $is_local
            {do
               {bands.lst set_provider {$this get_char_band_provider}}
               {song.lst set_provider {$this get_top_five_provider}}

               {if_else {> {$this get_band_count} 0}
                  {do
                     {$this enable bands.btn}
                     {bands.btn set_showing TRUE}
                     {bands.lst set_showing TRUE}
                  }
                  ; else
                  {do
                     {bands.btn set_showing FALSE}
                     {$this disable bands.btn}
                     {bands.lst set_showing FALSE}
                  }
               }

               {topsongs.btn set_showing TRUE}
               {song.lst set_showing TRUE}
            }
            ; else
            {do
               {$this disable bands.btn}
               {bands.btn set_showing FALSE}
               {topsongs.btn set_showing FALSE}
               {bands.lst set_showing FALSE}
               {song.lst set_showing FALSE}
            }
         }
      }

      {char_name.lbl set_localized {$this get_char_name}}
      {gamertag.btn set_localized {$this get_gamertag}}
      {cash.lbl set_localized {fmt_money {$this get_money}}}
      {motto.lbl set_localized {$this get_motto}}

      {do ($pcfg {game get_player_config [profile_player_num]})
         {instrument.lbl set_localized 
            {get_font_char_from_controller_type {$pcfg get_controller_type}}
         }
      }
   )

   (SELECT_MSG
      {switch $component
         (motto.btn
            {platform_mgr show_keyboard
               $player_num
               {get_motto_length}
               {localize tour_char_profile_vk_motto_title}
               {localize tour_char_profile_vk_motto_desc}
               {$this get_motto}
               $this
            }
         )
         (bands.btn
            {$this set_focus bands.lst}
         )
         (bands.lst
            {$this set_focus bands.btn}
         )
      }
   )

   (VIRTUAL_KEYBOARD_RESULT_MSG
      {if
         {&&
            $ok
            $this
            {ui focus_panel}
            {== {ui focus_panel} $this}
         }

         ; then

         {$this set_motto $text}
         {motto.lbl set_localized $text}

         {band_ui_sink show_network_busy_icon}
         {entity_uploader verify_band_motto $text $this}
      }
   )

   (BUTTON_DOWN_MSG
      {cond
         ({band_ui_sink is_network_busy}
            TRUE ;; eat message
         )
         ({== $button kPad_Tri}
            {if_else {== {$this focus_name} bands.lst}
               {do
                  {$this set_focus bands.btn}
                  TRUE
               }
               ; else
               kDataUnhandled
            }
         )
         (TRUE ;; else
            kDataUnhandled
         )
      }
   )

   (ROCK_CENTRAL_OP_COMPLETE_MSG
      {band_ui_sink hide_network_busy_icon}

      {switch $arg1
         (RB_RETCODE_CHAR_MOTTO_PROFANE
            {ui push_screen tour_char_motto_profane_screen}
         )
         (RB_RETCODE_CHAR_MOTTO_IP
            {ui push_screen tour_char_motto_ip_screen}
         )
         ; else (SUCCESS or SERVER_DOWN)
      }
   )
}

{new UIScreen tour_solo_char_profile_screen
   (panels
      meta
      practice_space_panel
      tour_char_profile_panel
      helpbar
   )
   (helpbar (helpbar_back helpbar_select))
   (focus tour_char_profile_panel)
   (back tour_solo_hub_screen)
}

{new UIScreen tour_band_char_profile_screen
   (panels
      meta
      practice_space_panel
      tour_char_profile_panel
      helpbar
   )
   (helpbar (helpbar_back helpbar_select))
   (focus tour_char_profile_panel)
   (back tour_band_hub_screen)
}

; bad char motto errors
{new UIScreen tour_char_motto_profane_screen
   (panels dialog_panel)
   (focus dialog_panel)
   LIMIT_TO_LEADER_SESSION
   (enter
      {dialog_panel set_ok {localize_token tour_char_profile_motto_profane}}
   )

   (SELECT_MSG
      {ui pop_screen}
   )
}

{new UIScreen tour_char_motto_ip_screen
   (panels dialog_panel)
   (focus dialog_panel)
   LIMIT_TO_LEADER_SESSION
   (enter
      {dialog_panel set_ok {localize_token tour_char_profile_motto_ip}}
   )

   (SELECT_MSG
      {ui pop_screen}
   )
}

; Solo Tour Choose Event/Song
{new UIPanel tour_solo_event_panel
   LIMIT_TO_LEADER_SESSION
   TOUR_EVENT_PANEL_HANDLERS
   COMMON_SONG_PREVIEW
   
   (file tour_solo_event.milo)
   (focus event.lst)
   
   (enter
      {$this refresh}
      {$this play_preview}
   )
   
   (exit
     ;; {song_preview ''} ; turn off song preview
      {session remove_sink $this}
   )
      
   (refresh
      {do ($provider {tour solo_event_provider})
         {event.lst set_provider $provider}
         {if {tour event}
            {event.lst set_selected {{tour event} get event_name} FALSE 
               {$provider refresh_first_showing}
            }
         }
      }
      ; always set event, to clear state
      {$this update_selection}
      
      ; trigger any new hints (really just for cheats)
      {if {tour hint}
         {ui goto_screen {{tour hint} get screen}}
      }
   )
   
   (update_selection
      {do ($provider {tour solo_event_provider})
          ($pos {event.lst selected_pos})
          ($town {$provider town $pos})
          ($venue {$provider venue $pos})
         
         {{tour performer} set_town $town}
         {{tour performer} set_venue $venue}
         {tour_world_bg_panel set town $town}
         {tour_world_bg_panel set venue $venue}
         {tour_world_bg_panel zoom_in}
         
         {unless {$this is_mode_selected}       
            {{tour performer} set_event 
               {event.lst selected_sym} 
               {game get leader_num}
            }               
         }
      }
   )
   
   (BUTTON_DOWN_MSG
      {cond
         ({== $button kPad_X}
            {do ($selected {event.lst selected_sym})
               {if_else {&& {! {$this is_mode_selected}}
                            {== kTourLocked {{tour performer} event_status}}
                        }
                  TRUE ; catch it (play invalid sfx sound?)
                  kDataUnhandled
               }
            }
         )
         ({== $button kPad_Tri}
            {do ($prev_mode {{tour solo_event_provider} get mode})
               {if_else {== $prev_mode kTourVenueNormal}
                  kDataUnhandled
                  {do
                     ; unselect mode, go back to normal event list
                     {synth play button_back}
                     {{tour solo_event_provider} set mode kTourVenueNormal}
                     {event.lst set_provider {tour solo_event_provider}}
                     {event.lst set_selected 
                        {if_else {== $prev_mode kTourVenueDownload}
                           download
                           unlock
                        }
                     }
                     {$this update_selection}
                     {$this play_preview}
                  }
               }   
            }
         )
         ({$this is_updown $button}
            {song_preview ''}
            kDataUnhandled
         )
         (TRUE kDataUnhandled)
      }
   )
   
   (BUTTON_UP_MSG
      {if {&& {! {ui in_transition}}
              {$this is_updown $button}}
         {$this play_preview}
      }
      kDataUnhandled
   )
   
   (play_preview
      {if_else {|| {$this is_mode_selected}
                   {== kTourLocked {{tour performer} event_status}}}
         {song_preview ''} ; turn off song preview
         {song_preview 
            {{tour solo_event_provider} event_song {event.lst selected_pos}}
         }
      }
   )
   
   (is_mode_selected
      {|| {== unlock {event.lst selected_sym}}
          {== download {event.lst selected_sym}}}
   )
   
   (SCROLL_MSG
      {if {!= {{tour solo_event_provider} town {event.lst selected_pos}}
              {{tour performer} town}
          }
         {synth play bwt_swish.cue}
      }
      {$this update_selection}   
   )
   
   (SELECT_START_MSG
      {if {&& {!= {event.lst selected_sym} unlock}
              {!= {event.lst selected_sym} download}
              {!= kTourLocked {{tour performer} event_status}}
          }
         {$this confirm_event}
        
         ; initialize player's track type
         {do ($pcfg {game get_player_config {game get leader_num}})
            {$pcfg set_track_type {{$pcfg get_char} get_instrument}}
         }
         {session add_sink $this}
         {{game get_player_config {game get leader_num}} set_ready TRUE}
      }
      kDataUnhandled 
   )
   
   ; called as a result of marking player ready
   (start_loading_msg
      {ui goto_screen loading_screen}   
   )
   
   (SELECT_MSG
      {switch {event.lst selected_sym}
         (unlock
            {{tour solo_event_provider} set mode kTourVenueUnlock}
            {$this refresh}
            {$this play_preview}
         )
         (download
            {{tour solo_event_provider} set mode kTourVenueDownload}
            {$this refresh}   
            {$this play_preview}
         )
      }
   )
}

{new UIScreen tour_solo_event_screen
   (panels meta tour_world_bg_panel tour_solo_event_panel helpbar)
   (focus tour_solo_event_panel)   
   (back tour_solo_hub_screen)   
   (helpbar (helpbar_back helpbar_select))
   
   (enter
		{helpbar set_limit_to_player_num {game get leader_num}}
   )
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Solo Tour Results Screen
{new UIPanel tour_solo_endgame_panel
   (file "tour_solo_endgame.milo") ; TEMP
   (focus continue.btn)
   ENDGAME_PANEL_HANDLERS
   
   (enter
      ; no need to autosave here, we're not done with event yet
      {$this set_results}
      {$this submit_achievements}
   )
   
   (SELECT_MSG
      {ui goto_screen tour_solo_gigcomplete_screen}
   )
}

{new UIScreen tour_solo_endgame_screen
   (panels tour_solo_endgame_panel helpbar)
   (focus  tour_solo_endgame_panel)
   (helpbar ('' helpbar_finishsong))
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Solo Tour Gig Complete
{new UIPanel tour_solo_gigcomplete_panel
   (file "tour_solo_gigcomplete.milo")
   (focus continue.btn)
   LIMIT_TO_LEADER_SESSION
      
   (enter   
      {do ($tp {tour performer})
          ($performer {beatmatch active_player 0})
          ($event {tour event})

         ; star rating
         {stars.lbl set_localized {$performer star_rating}}

         ; score
         {score.lbl set_localized {fmt_score {$performer score}}}
         
         ; $ reward! (always exists)
         {money.lbl set_localized 
            {sprintf
               {localize tour_solo_reward}
               {fmt_money {$tp reward}}
            }
         }
         
         ; outfit!
         {do ($instrument {$tp get_instrument})
             ($gender     {$tp get_gender})
             ($category   {$event unlock_category $instrument $gender})
             ($outfit     {$event unlock_outfit   $instrument $gender})
             ($line_idx 0) ; index of icon/label    
               
            {if {&& {!= $category ''}
                    {! {$tp has_outfit $category $outfit}}} 
               {{$this icon $line_idx} set_showing TRUE}
               {{$this icon $line_idx} set_mat icon_outfit.mat}
               
               {{$this line $line_idx} set_showing TRUE}
               {{$this line $line_idx} set_localized
                  {sprintf {localize tour_solo_outfit}
                     {localize_token 
                        {shop find_token $gender $category $outfit}
                     }
                  }
               }
               
               {++ $line_idx}
            }
         
            ; now complete the event, awarding songs, money, items...
            {{tour performer} complete_event}
   
            {cond
               ; new town!
               ; FOR CORE DISC SONGS, NOT FOR UNLOCK DISC SONGS
               ({!= {$tp new_town} ''}
                  {{$this icon $line_idx} set_showing TRUE}
                  {{$this icon $line_idx} set_mat icon_newtown.mat}

                  {{$this line $line_idx} set_showing TRUE}
                  {{$this line $line_idx} set_localized
                     {sprintf 
                        {localize tour_endgame_newtown} 
                        {localize {$tp new_town}}
                     }
                  }
                  
                  {++ $line_idx}
               )
               ; new unlock songs!
               ;; FOR UNLOCK DISC SONGS, NOT FOR CORE DISC
               ({!= {$tp new_unlock} ''}
                  {{$this icon $line_idx} set_showing TRUE}
                  {{$this icon $line_idx} set_mat icon_newtown.mat}

                  {{$this line $line_idx} set_showing TRUE}
                  {{$this line $line_idx} set_localized
                     {sprintf 
                        {localize tour_solo_newsong}
                        {song_mgr song_name {$tp new_unlock}}
                     }
                  }
                  
                  {++ $line_idx}
               )
            }
            
            ; hide unused icons/lines
            {foreach_int $i $line_idx 2
               {{$this icon $i} set_showing FALSE}
               {{$this line $i} set_showing FALSE}
            }
         }
            
         ; logging, assumes single song events
         {log_tour "EVENT   " {sprintf "WON $%d"  
            {{tour performer} reward}}}
         {log_tour "EVENT   " {sprintf "WON *x%d" {$performer num_stars}}}
         
         ; unlocked venue type achievement
         {foreach $venue (big_club theater arena)
            {if {$tp is_venue_type_unlocked $venue}
               {achievements submit {game get leader_num}
                  {symbol {sprintf "ach_solo_venue_%s" $venue}}
               }
            }
         }
      }  
   )
   
   (line ($idx)
      {sprintf "line%i.lbl" $idx}   
   )
   
   (icon ($idx)
      {sprintf "icon%i.mesh" $idx}  
   )
 
   (SELECT_START_MSG
      {ui goto_screen
         {if_else {{tour performer} is_winning}
            meta_loading_tour_win_screen
            meta_loading_continue_screen
         }
      }
      kDataUnhandled
   )
}


{new UIScreen tour_solo_gigcomplete_screen
   (panels tour_solo_gigcomplete_panel helpbar)
   (focus tour_solo_gigcomplete_panel)
   (helpbar ('' helpbar_returntomap))
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;   Win the game sequence screens

#define SOLO_TOUR_ACHIEVEMENT
(
   (award_solo_tour_achievement
      {achievements submit {game get leader_num}
         {symbol {sprintf "ach_solo_%s_%s"
            {{tour performer} get_instrument}
            {{game get_player_config {game get leader_num}} get_difficulty_sym}
         }}
      }
   )
)

{new UIPanel tour_solo_congrats_easy_panel
   (file "tour_solo_congrats_easy.milo")
   LIMIT_TO_LEADER_SESSION
   SOLO_TOUR_ACHIEVEMENT
   (enter
      {$this award_solo_tour_achievement}
   )
   (BUTTON_DOWN_MSG
      {if {|| {== $button kPad_Start}
              {== $button kPad_X}}
         {ui goto_screen tour_solo_video_credits_screen}
      }   
   )
}
{new UIScreen tour_solo_easy_win_screen
   (panels tour_solo_congrats_easy_panel helpbar)
   (focus tour_solo_congrats_easy_panel)
   (helpbar ('' helpbar_continue))
}


{new UIPanel tour_solo_congrats_panel
   (file "tour_solo_congrats.milo")
   (focus continue.btn)
   LIMIT_TO_LEADER_SESSION
   SOLO_TOUR_ACHIEVEMENT
   (enter
      {$this award_solo_tour_achievement}
   )
   (BUTTON_DOWN_MSG
      {if {|| {== $button kPad_Start}
              {== $button kPad_X}}
         {ui goto_screen tour_solo_video_credits_screen}
      }   
   )
}
{new UIScreen tour_solo_win_screen
   (panels tour_solo_congrats_panel helpbar)
   (focus tour_solo_congrats_panel)
   (helpbar ('' helpbar_continue))
}

{new MoviePanel tour_solo_video_credits_panel
   (preload FALSE) ; stream it
   (audio TRUE)
   (loop FALSE)
   (update_presence TRUE) ; set presence to "Watching Movie"
   (videos win_credits)
   LIMIT_TO_LEADER_SESSION
   ; callback when movie finishes      
   (movie_done
      {ui goto_screen tour_solo_text_credits_screen}  
   )
   
   (BUTTON_DOWN_MSG
      {if {|| {== $button kPad_Start}
              {== $button kPad_X}}
         {ui goto_screen tour_solo_text_credits_screen}
      }
   )
}
{new UIScreen tour_solo_video_credits_screen
   (panels tour_solo_video_credits_panel)
}

{new UIScreen tour_solo_text_credits_screen
   (panels credits_panel)
   (scroll_sfx FALSE) ; don't play scroll sfx in this screen
   LIMIT_TO_LEADER_SESSION
   (BUTTON_DOWN_MSG
      {if {|| {== $button kPad_Start}
              {== $button kPad_X}}
         {ui goto_screen tour_solo_item_reward_screen}
      }   
   )
}

{new UIPanel tour_solo_item_reward_panel
   (file "tour_solo_item_rewards.milo")
   (focus continue.btn)
   LIMIT_TO_LEADER_SESSION
   (BUTTON_DOWN_MSG
      {if {|| {== $button kPad_Start}
              {== $button kPad_X}}
         {ui goto_screen tour_solo_sendoff_screen}
      }   
   )
}
{new UIScreen tour_solo_item_reward_screen
   (panels tour_solo_item_reward_panel helpbar)
   (focus tour_solo_item_reward_panel)
   (helpbar ('' helpbar_continue))
}

{new UIPanel tour_solo_sendoff_panel
   (file "tour_solo_win_sendoff.milo")
   (focus continue.btn)
   LIMIT_TO_LEADER_SESSION
   (BUTTON_DOWN_MSG
      {if {|| {== $button kPad_Start}
              {== $button kPad_X}}
         {ui goto_screen {gamemode get continue_screen}}
      }   
   )
}
{new UIScreen tour_solo_sendoff_screen
   (panels tour_solo_sendoff_panel helpbar)
   (focus tour_solo_sendoff_panel)
   (helpbar ('' helpbar_continue))
}

;;;;;;;;;;;;;;;;;;;;;;;;
; Solo Tour Hints
#define SOLO_TOUR_NEW_TOWN_UPDATE
(
   {do ($town {{tour performer} new_town})
      {if {!= $town ''}
         ; set the TourChar's town too
         {{tour performer} set_town $town}

         ; need to refresh it with the new events
         {{tour solo_event_provider} refresh}
         
         ; update the TourChar's event so we select it in the list
         {{tour performer} set_event 
            {{tour solo_event_provider} first_event $town}
            {game get leader_num}
         }
      }
   }    
   ; make sure we're on top of the current town     
   {tour_world_bg_panel set town {{tour performer} town}}
   {tour_world_bg_panel zoom_out}
   {tour_world_bg_panel update}
)

;;;;;;;;;;;;;;;;;;;;;;;;
; New town unlocked hint
{new UIPanel tour_solo_newtown_panel
   (file "tour_solo_newtown.milo")
   TOUR_HINT_PANEL_HANDLERS
   
   (enter
      SOLO_TOUR_NEW_TOWN_UPDATE     
      {town.lbl set_localized
         {sprintf {localize tour_solo_unlocked} 
            {localize_token {{tour performer} new_town}}
         }
      }
   )
}

{new UIScreen tour_solo_newtown_screen
   (panels meta tour_world_bg_panel tour_solo_newtown_panel helpbar)
   (focus tour_solo_newtown_panel)
   (helpbar ('' helpbar_continue))
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; New unlock song unlocked hint
{new UIPanel tour_solo_newunlock_panel
   (file "tour_solo_newsong.milo") ; need a different file?
   TOUR_HINT_PANEL_HANDLERS
   (enter
      {do ($town {{tour performer} unlock_town})
         {{tour performer} set_town $town}
         
         ; need to refresh it with the new events
         {{tour solo_event_provider} set mode kTourVenueUnlock}
         {{tour solo_event_provider} refresh}      
         
         ; select the latest event so we select it in the list
         {{tour performer} set_event 
            {{tour solo_event_provider} last_event $town}
            {game get leader_num}
         }
   
         {town.lbl set_localized
            {sprintf
               {localize tour_solo_newsong}
               {song_mgr song_name {{tour performer} new_unlock}}
            }
         }   
            
         ; make sure we're on top of the unlock town     
         {tour_world_bg_panel set town {{tour performer} town}}
         {tour_world_bg_panel zoom_out}
         {tour_world_bg_panel update}
      }
   )
}

{new UIScreen tour_solo_newunlock_screen
   (panels meta tour_world_bg_panel tour_solo_newunlock_panel helpbar)
   (focus tour_solo_newunlock_panel)
   (helpbar ('' helpbar_continue))
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Sponsor screens
#define SOLO_TOUR_SPONSOR_HANDLERS
(
   TOUR_HINT_PANEL_HANDLERS
   (enter
      SOLO_TOUR_NEW_TOWN_UPDATE
   )
)

; Guitar Welcome
{new UIPanel tour_solo_guitar_welcome_panel
   (file "tour_solo_guitar_welcome.milo")
   SOLO_TOUR_SPONSOR_HANDLERS
}

{new UIScreen tour_solo_guitar_welcome_screen
   (panels meta tour_world_bg_panel tour_solo_guitar_welcome_panel helpbar)
   (focus tour_solo_guitar_welcome_panel)
   (helpbar ('' helpbar_continue))
}

; Drum Welcome
{new UIPanel tour_solo_drum_welcome_panel
   (file "tour_solo_drum_welcome.milo")
   SOLO_TOUR_SPONSOR_HANDLERS
}

{new UIScreen tour_solo_drum_welcome_screen
   (panels meta tour_world_bg_panel tour_solo_drum_welcome_panel helpbar)
   (focus tour_solo_drum_welcome_panel)
   (helpbar ('' helpbar_continue))
}

; Vocal Welcome
{new UIPanel tour_solo_vocal_welcome_panel
   (file "tour_solo_vocal_welcome.milo")
   SOLO_TOUR_SPONSOR_HANDLERS
}

{new UIScreen tour_solo_vocal_welcome_screen
   (panels meta tour_world_bg_panel tour_solo_vocal_welcome_panel helpbar)
   (focus tour_solo_vocal_welcome_panel)
   (helpbar ('' helpbar_continue))
}

; Guitar Big Club
{new UIPanel tour_solo_guitar_sponsorbig_panel
   (file "tour_solo_guitar_sponsorbig.milo")
   SOLO_TOUR_SPONSOR_HANDLERS
}

{new UIScreen tour_solo_guitar_sponsorbig_screen
   (panels meta tour_world_bg_panel tour_solo_guitar_sponsorbig_panel helpbar)
   (focus tour_solo_guitar_sponsorbig_panel)
   (helpbar ('' helpbar_continue))
}

; Drum Big Club
{new UIPanel tour_solo_drum_sponsorbig_panel
   (file "tour_solo_drum_sponsorbig.milo")
   SOLO_TOUR_SPONSOR_HANDLERS
}

{new UIScreen tour_solo_drum_sponsorbig_screen
   (panels meta tour_world_bg_panel tour_solo_drum_sponsorbig_panel helpbar)
   (focus tour_solo_drum_sponsorbig_panel)
   (helpbar ('' helpbar_continue))
}

; Vocal Big Club
{new UIPanel tour_solo_vocal_sponsorbig_panel
   (file "tour_solo_vocal_sponsorbig.milo")
   SOLO_TOUR_SPONSOR_HANDLERS
}

{new UIScreen tour_solo_vocal_sponsorbig_screen
   (panels meta tour_world_bg_panel tour_solo_vocal_sponsorbig_panel helpbar)
   (focus tour_solo_vocal_sponsorbig_panel)
   (helpbar ('' helpbar_continue))
}

; Guitar Arena
{new UIPanel tour_solo_guitar_sponsorarena_panel
   (file "tour_solo_guitar_sponsorarena.milo")
   SOLO_TOUR_SPONSOR_HANDLERS
}

{new UIScreen tour_solo_guitar_sponsorarena_screen
   (panels meta tour_world_bg_panel tour_solo_guitar_sponsorarena_panel helpbar)
   (focus tour_solo_guitar_sponsorarena_panel)
   (helpbar ('' helpbar_continue))
}

; Drum Big Club
{new UIPanel tour_solo_drum_sponsorarena_panel
   (file "tour_solo_drum_sponsorarena.milo")
   SOLO_TOUR_SPONSOR_HANDLERS
}

{new UIScreen tour_solo_drum_sponsorarena_screen
   (panels meta tour_world_bg_panel tour_solo_drum_sponsorarena_panel helpbar)
   (focus tour_solo_drum_sponsorarena_panel)
   (helpbar ('' helpbar_continue))
}

; Vocal Big Club
{new UIPanel tour_solo_vocal_sponsorarena_panel
   (file "tour_solo_vocal_sponsorarena.milo")
   SOLO_TOUR_SPONSOR_HANDLERS
}

{new UIScreen tour_solo_vocal_sponsorarena_screen
   (panels meta tour_world_bg_panel tour_solo_vocal_sponsorarena_panel helpbar)
   (focus tour_solo_vocal_sponsorarena_panel)
   (helpbar ('' helpbar_continue))
}
