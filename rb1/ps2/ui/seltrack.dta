;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Call this to init TrackTypes properly and then proceed to the
;;   appropriate screen.
{func hth_set_track_types
   {gamecfg foreach_player_config $pcfg
      {if {$pcfg is_local_player}
         {if_else
            ; if bass not allowed, or it is but controller isn't a guitar
            {||
               {! {gamemode get allow_bass}}
               {!= {$pcfg get_controller_type} kControllerGuitar}
            }

            ; then auto choose the track type
            {$pcfg set_track_type {$pcfg get_controller_sym}}

            ; else, clear the track type
            {$pcfg set_track_type kTrackNone}
         }
      }
   }
}

{func hth_goto_tracksel_screen
   ;; set local track types (local to leader's machine)
   {hth_set_track_types}

   ;; set remote track types (eveyone else's machines)
   {session send_to_all (hth_set_track_types) kNetReliable}

   {ui goto_screen
      {if_else
         ; if bass not allowed, or it is but no guitars...
         {&&
            {gamemode get allow_bass}
            {gamecfg has_participating_guitar_controller}
            {gamemode get separate_parts}
         }
         qp_hth_seltrack_screen
         {gamemode get ready_screen}
      }
   }
}

; head-to-head track selection
{new UIPanel
   qp_hth_seltrack_panel
   #ifdef HX_XBOX
   (file "qp_hth_seltrack.milo")
   #else
   (file "qp_hth_seltrack_og.milo")
   #endif
   LIMIT_TO_LEADER_SESSION
   (focus guitar.btn)

   (enter
      {if
         {==
            {{game get_player_config {game get leader_num}} get_track_type}
            kTrackNone
         }
         {$this set_focus guitar.btn}
      }
   )

   (SELECT_MSG
      {do
         ($track {basename {$component name}})
         {game foreach_player_config $pcfg
            {if {$pcfg is_participating}
               {if_else {$pcfg is_local_player}
                  {$pcfg set_track_type $track}
                  ; HACK to work in net game
                  {do
                     ($player_num {$pcfg get player_num})
                     {session send_to_player
                        $player_num
                        {`
                           ({game get_player_config {, $player_num}}
                              set_track_type
                              {, $track}
                           )
                        }
                        kNetReliable
                     }
                  }
               }
            }
         }
      }
      {ui goto_screen {gamemode get ready_screen}}
   )
}

{new UIScreen
   qp_hth_seltrack_screen
   (panels meta postsong_sfx_panel movie_panel qp_hth_seltrack_panel helpbar)
   (focus qp_hth_seltrack_panel)
   (helpbar
      {if_else {is_leader_local}
         (helpbar_back helpbar_select)
         (helpbar_back)
      }
   )
   HELPBAR_WAITING_FOR_LEADER
   LIMIT_TO_LEADER_SESSION
   (back qp_selsong_screen)
}