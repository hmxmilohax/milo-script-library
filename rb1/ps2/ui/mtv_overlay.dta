
{new UIPanel mtv_overlay
   (file mtv_overlay.milo)
   (enter
      {$this show_overlay FALSE}
      {if {exists game}
         {game add_sinks $this intro_end}
      }
   )
   
   ; used to show 1 second after intro_start and hide at -.5 seconds.
   ; before time zero.
   (intro_end   
      {if {gamemode get show_mtv_overlay}
         {$this setup_text}
         {$this show_overlay TRUE}
         { script_task kTaskSeconds (delay 5) 
            (script {$this show_overlay FALSE})
         }
      }
   )
   
   (exit
      {if {exists game}
         {game remove_sink $this}
      }
   )

   (setup_text
      {do  ($song        {game get_song})
           ($song_text   {song_mgr song_name $song})
           ($artist_text {song_mgr song_artist $song})
         
         {if_else {song_mgr is_master $song}
            {do ($prefix "mtv_master_line")
               {mtv_cover_song_id.view set_showing FALSE}
               {mtv_master_song_id.view set_showing TRUE}
               {$this set_line $prefix 1 $song_text}
               {$this set_line $prefix 3 $artist_text}

            }
            {do ($prefix "mtv_cover_line")
               {mtv_cover_song_id.view set_showing TRUE}
               {mtv_master_song_id.view set_showing FALSE}
               {$this set_line $prefix 1 $song_text}
               {$this set_line $prefix 3 $artist_text}
            }
         }
      }
   )

   (set_line ($prefix $i $text)
      {{sprint $prefix $i ".lbl"} set_localized $text}
      ;{{sprint $prefix $i "_shadow.lbl"} set_localized $text}
   )

   (show_overlay ($show)      
      {$this set_showing $show}
      {if_else $show {fade.anim animate} {fade.anim set_frame 0}}
      {$this set_paused {! $show}}
   )
}
   
