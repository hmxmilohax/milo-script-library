
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Band Select screen

{new UIPanel tour_band_sel_panel
   (file "tour_band_sel.milo")
   (focus band.lst)

   (band_provider 0)

   (enter
      ; note that clearing the session removes
      ; all players, thus resetting their data
      ;{session clear} HOJ removed this to get helpbar working

      ; clear any lingering unfinalized bands...
      {tour clear_performer}

      {set [band_provider] {get_band_provider}}
      {band.lst set_provider [band_provider]}

      {session add_sink $this signin_changed}
      {memcardmgr add_sink $this save_load_all_complete_msg}
      {profile_mgr add_sink $this profile_activated_msg}
   )

   (TRANSITION_COMPLETE_MSG
      {autoload}
   )

   (exit
      {session remove_sink $this signin_changed}
      {memcardmgr remove_sink $this save_load_all_complete_msg}
      {profile_mgr remove_sink $this profile_activated_msg}
   )

   (BUTTON_DOWN_MSG
      {switch $button
         (kPad_R1
            {if {! {platform_mgr is_player_signed_in $player_num}}
               {platform_mgr signin 1}
            }
         )
         kDataUnhandled
      }
   )

   (SELECT_MSG
      {switch $component
         (band.lst
            {do ($pos {band.lst selected_pos})
               {cond
                  ; create a new band
                  ({[band_provider] is_pos_create_band $pos}
                     {tour set_performer {tour create_new_band}}
                     {tour set_bwtmode kMetaTour_StartNew}
                     {ui goto_screen {gamemode get matchmaking_screen}}
                  )
                  ; join an existing band
                  ({[band_provider] is_pos_join_band $pos}
                     {tour set_bwtmode kMetaTour_UnknownRemote}
                     {ui goto_screen {gamemode get matchmaking_screen}}
                  )
                  ; ELSE, they actually chose a band
                  (TRUE
                     {if_else {[band_provider] is_band_local $pos}
                        {do
                           ; set the provider since we have the master save data
                           {tour set_performer {[band_provider] get_band $pos}}

                           {tour set_bwtmode kMetaTour_KnownLocal}
                        }
                        ; else set the band info necessary to connect later on the matchmaking screen   
                        {do
                           {[band_provider] select_remote_band $pos}

                           {tour set_bwtmode kMetaTour_KnownRemote}
                        }
                     }

                     ; refresh, so we see the band logo
                     {practice_space_panel refresh}
         
                     {ui goto_screen {gamemode get matchmaking_screen}}
                  )
               }
            }
         )
      }
   )

   (JOYPAD_CONNECT_MSG
      ;; Refresh the display by re-setting the provider
      {band.lst set_provider [band_provider]}
   )

   (signin_changed
      {autoload}
      ;; Refresh the display by re-setting the provider
      {band.lst set_provider [band_provider]}
   )

   (save_load_all_complete_msg
      ;; Refresh the display by re-setting the provider
      {band.lst set_provider [band_provider]}
   )
   
   (profile_activated_msg ($pad_num)
      {autoload}
      ;; Refresh the display by re-setting the provider
      {band.lst set_provider [band_provider]}
   )
}


{new UIScreen tour_band_sel_screen
   (panels meta practice_space_panel tour_band_sel_panel helpbar)
   (helpbar (helpbar_back helpbar_selband))
   (focus tour_band_sel_panel)

   ;stubbed out until Band World Tour Online enabled
   ;(back main_worldtour_screen) 
   (back main_multimode_screen)

   (enter
      ;; update rich presence...
      {presence_mgr set_assembling_band}

      ; select the coop matchmaking shot
      {practice_space_panel select_cam "coop_enter"}
   )
   
   (exit
      ;; update rich presence...
      {presence_mgr set_prepare_to_rock}
   )
}


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Band Lineup/Roster screen

{new BandLineupPanel tour_band_lineup_panel
   (file "tour_band_lineup.milo")
   (focus player00.btn)
   (leader_row -1)
   (force_exit TRUE) ; force exit/re-enter (to band roster)

   LIMIT_TO_SESSION

   (enter
      {practice_space_panel select_cam "coop_enter"}

      {band_name.lbl set_localized {{tour performer} band_name}}

      {$this update}
   )

   (update
      {$this build_resolution_list}
      
      {foreach $i (0 1 2 3)
         {do
            ($rowArray {$this get_row $i})
            ($is_visible TRUE)

            ; 0 - isLeader
            {{sprintf "leader%02d.lbl" $i} set_showing {elem $rowArray 0}}
            {printf "%i isLeader %i\n" $i {elem $rowArray 0}}
            {if {elem $rowArray 0} 
               {set [leader_row] $i}
            }

            ; 1 - instrument
            {{sprintf "instrument%02d.lbl" $i}
               set_localized
               {cnv_instrumenttoicon {elem $rowArray 1}}
            }

            ; 2 - Original Name            
            {{sprintf "original%02d.lbl" $i} set_localized {elem $rowArray 2}}

            ; 3 - state
            ; 4 - Sub Name
            {switch {elem $rowArray 3}

               (kMetaLineupEmpty
                  {set $is_visible FALSE}
               )
               (kMetaLineupPlaying
                  {set $is_visible TRUE}
                  {{sprintf "status%02d.lbl" $i}
                     set
                     text_token
                     tour_band_lineup_status_playing
                  }
               )
               (kMetaLineupOffline
                  {set $is_visible TRUE}
                  {{sprintf "status%02d.lbl" $i}
                     set
                     text_token
                     tour_band_lineup_status_offline
                  }
               )
               (kMetaLineupSubOnly
                  {set $is_visible TRUE}
                  {{sprintf "status%02d.lbl" $i}
                     set_localized
                     {sprintf {localize tour_band_lineup_status_sub_fmt}
                        {elem $rowArray 4}
                     }
                  }
               )
               (kMetaLineupSubForOffline
                  {set $is_visible TRUE}
                  {{sprintf "status%02d.lbl" $i}
                     set_localized
                     {sprintf {localize tour_band_lineup_status_sub_fmt}
                        {elem $rowArray 4}
                     }
                  }
               )
            }

            ; Is Visible
            {$this
               {if_else $is_visible
                  enable
                  ; else
                  disable
               }
               {sprintf "player%02d.btn" $i}
            }
            {{sprintf "player%02d.grp" $i} set_showing $is_visible}
         }
      }
   )

   (SELECT_START_MSG
      {do
         ($row_num 
            {switch $component
               (player00.btn 0)
               (player01.btn 1)
               (player02.btn 2)
               (player03.btn 3)
            }
         )

         ;; see if we can do anything before actually trying to do anything...

         {do
            {tour_band_lineup_menu_panel set row_num $row_num}
            {ui goto_screen tour_band_lineup_menu_screen}
            kDataUnhandled
         }
      }
   )
}

{new UIScreen tour_band_lineup_screen
   (panels meta practice_space_panel tour_band_lineup_panel
      tour_band_lower3rd_panel helpbar
   )
   (helpbar (helpbar_back helpbar_continue))
   (focus tour_band_lineup_panel)
   (back tour_band_hub_screen)
}

{new UIPanel tour_band_lineup_menu_panel
   (file "tour_band_lineup_menu.milo")
   (focus opt0.btn)

   (player_num -1)
   (row_num -1)

   (setup ($player_num $row_num)
      {set [player_num] $player_num}
      {set [row_num] $row_num}
   )

   (enter
      {do
         ($rowArray {tour_band_lineup_panel get_row [row_num]})
         ($is_leader {== {tour_band_lineup_panel get leader_row} [row_num]})
         ($is_promotable {== {elem $rowArray 3} kMetaLineupSubOnly})
         ($is_demotable {== {elem $rowArray 3} kMetaLineupPlaying})
         ($is_missing
            {||
               {== {elem $rowArray 3} kMetaLineupOffline}
               {== {elem $rowArray 3} kMetaLineupSubForOffline}
            }
         )
         ($char_name
            {if_else {== {elem $rowArray 3} kMetaLineupSubOnly}
               {elem $rowArray 4}
               ; else
               {elem $rowArray 2}
            }
         )

         {$this enable opt1.btn}
         {opt1.btn set_showing TRUE}

         {cond
            ($is_leader
               ; view char profile
               {opt0.btn set text_token tour_band_lineup_view_char_profile}

               ; <no second option>
               {$this disable opt1.btn}
               {opt1.btn set_showing FALSE}
            )
            ($is_promotable
               ; make perm
               {opt0.btn set text_token tour_band_lineup_promote}
               ; view char profile
               {opt1.btn set text_token tour_band_lineup_view_char_profile}
            )
            ($is_demotable
               ; demote
               {opt0.btn set text_token tour_band_lineup_demote}
               ; view char profile
               {opt1.btn set text_token tour_band_lineup_view_char_profile}
            )
            ($is_missing
               ; demote (missing)
               {opt0.btn set text_token tour_band_lineup_demote_missing}
               
               ; <no second option>
               {$this disable opt1.btn}
               {opt1.btn set_showing FALSE}
            )
            ; ELSE
            (TRUE
               ; view char profile
               {opt0.btn set text_token tour_band_lineup_view_char_profile}
               ; <no second option>
               {$this disable opt1.btn}
               {opt1.btn set_showing FALSE}
            )
         }

         {charname.lbl set_localized $char_name}
      }
   )

   (SELECT_MSG
      {switch {$component get text_token}
         (tour_band_lineup_demote
            {ui push_screen tour_band_lineup_demote_confirm_screen}
         )
         (tour_band_lineup_demote_missing
            {ui push_screen tour_band_lineup_demote_missing_confirm_screen}
         )
         (tour_band_lineup_promote
            {if_else
               {profile_mgr can_char_join_band
                  {{gamecfg get_player_config {tour_band_lineup_panel get_player_num_from_row [row_num]}}
                     get_char
                  }
               }
               ; then
               {ui push_screen tour_band_lineup_promote_confirm_screen}
               ; else
               {ui push_screen tour_band_lineup_at_band_limit_screen}
            }
         )
         (tour_band_lineup_view_char_profile
            {tour_char_profile_panel set profile_player_num
               {tour_band_lineup_panel get_player_num_from_row [row_num]}
            }

            {ui goto_screen tour_band_lineup_char_profile_screen}
         )
      }
   )
}

{new UIScreen tour_band_lineup_menu_screen
   (panels
      meta
      practice_space_panel
      tour_band_lineup_panel
      tour_band_lineup_menu_panel
      helpbar
   )
   (helpbar (helpbar_back helpbar_continue))
   (focus tour_band_lineup_menu_panel)
   (back tour_band_lineup_screen)
}

{new UIScreen tour_band_lineup_char_profile_screen
   (panels
      meta
      practice_space_panel
      tour_band_lineup_panel
      tour_char_profile_panel
      helpbar
   )
   (helpbar (helpbar_back helpbar_select))
   (focus tour_char_profile_panel)
   (back tour_band_lineup_screen)
}

;; demote confirmation
{new UIScreen tour_band_lineup_demote_confirm_screen
   (panels dialog_panel)
   (focus dialog_panel)

   (enter
      {do
         ($rowArray
            {tour_band_lineup_panel get_row
               {tour_band_lineup_menu_panel get row_num}
            }
         )
         ($char_name
            {if_else {== {elem $rowArray 3} kMetaLineupSubOnly}
               {elem $rowArray 4}
               ; else
               {elem $rowArray 2}
            }
         )

         {dialog_panel set_yesno 
            {sprintf
               {localize tour_band_lineup_confirm_demote_fmt}
               $char_name
            }
            no.btn
         }
      }
   )

   (SELECT_MSG
      {switch $component
         (yes.btn
            {tour_band_lineup_panel demote_member_to_sub
               {tour_band_lineup_menu_panel get row_num}
            }
            {ui pop_screen tour_band_lineup_screen}
         )
         (no.btn
            {ui pop_screen}
         )
      }
   )
}

;; demote missing member confirmation
{new UIScreen tour_band_lineup_demote_missing_confirm_screen
   (panels dialog_panel)
   (focus dialog_panel)

   (enter
      {do
         ($rowArray
            {tour_band_lineup_panel get_row
               {tour_band_lineup_menu_panel get row_num}
            }
         )
         ($char_name
            {if_else {== {elem $rowArray 3} kMetaLineupSubOnly}
               {elem $rowArray 4}
               ; else
               {elem $rowArray 2}
            }
         )

         {dialog_panel set_yesno 
            {sprintf
               {localize tour_band_lineup_confirm_demote_missing_fmt}
               $char_name
            }
            no.btn
         }
      }
   )

   (SELECT_MSG
      {switch $component
         (yes.btn
            {tour_band_lineup_panel demote_offline_member
               {tour_band_lineup_menu_panel get row_num}
            }
            {ui pop_screen tour_band_lineup_screen}
         )
         (no.btn
            {ui pop_screen}
         )
      }
   )
}

;; promote member confirmation
{new UIScreen tour_band_lineup_promote_confirm_screen
   (panels dialog_panel)
   (focus dialog_panel)

   (enter
      {do
         ($rowArray
            {tour_band_lineup_panel get_row
               {tour_band_lineup_menu_panel get row_num}
            }
         )
         ($char_name
            {if_else {== {elem $rowArray 3} kMetaLineupSubOnly}
               {elem $rowArray 4}
               ; else
               {elem $rowArray 2}
            }
         )

         {dialog_panel set_yesno 
            {sprintf
               {localize tour_band_lineup_confirm_promote_fmt}
               $char_name
            }
            yes.btn
         }
      }
   )

   (SELECT_MSG
      {switch $component
         (yes.btn
            {tour_band_lineup_panel promote_sub_to_member
               {tour_band_lineup_menu_panel get row_num}
            }
            {ui pop_screen tour_band_lineup_screen}
         )
         (no.btn
            {ui pop_screen}
         )
      }
   )
}

;; Can't promote, too many memberships already!
{new UIScreen tour_band_lineup_at_band_limit_screen
   (panels dialog_panel)
   (focus dialog_panel)

   (enter
      {do
         ($rowArray
            {tour_band_lineup_panel get_row
               {tour_band_lineup_menu_panel get row_num}
            }
         )
         ($char_name
            {if_else {== {elem $rowArray 3} kMetaLineupSubOnly}
               {elem $rowArray 4}
               ; else
               {elem $rowArray 2}
            }
         )

         {dialog_panel set_ok
            {sprintf {localize tour_band_lineup_at_band_limit_fmt}
               $char_name
               {profile_mgr get_max_bands_per_char}
            }
         }
      }
   )

   (SELECT_MSG
      {ui pop_screen}
   )
}
