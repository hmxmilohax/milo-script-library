; plays loading animation
{new UIPanel meta_loading
   (file meta_loading.milo)
}

; general end of game, accept invite, etc. autosave
#define AUTOSAVE_WAIT_SCREEN_HANDLERS
(
   (next_screen '')

   (enter
      ; we're done autosaving, stop waiting
      {game foreach_local_player_config $pcfg
         {$pcfg set_ui_waiting FALSE}         
      }
      {session add_sink $this}
      {$this check_advance}
   )
   
   (exit
      {session remove_sink $this}
   )
   
   (REMOTE_PLAYER_CHANGED_MSG
      {$this check_advance}
   )

   (REMOTE_PLAYER_LEFT_MSG
      {unless {session disabled}
         {$this check_advance} ; don't check again if we just cleared the session
      }
   )

   (check_advance   
      {do ($ready TRUE)
         {game foreach_player_config $pcfg
            {if {&&
                  {session has_player {$pcfg get player_num}} ; only care if still connected
                  {$pcfg ui_waiting}
                  {! {gamemode get ranked}} ; always progress if we're in a ranked game
                }
               {set $ready FALSE}   
            }
         }
         {if $ready
            ; we had to disable this before we got here
            {band_ui_sink set_net_sync TRUE}
            {ui goto_screen [next_screen]}
         }
      }
   )
)

{new UIScreen autosave_wait_screen  
   (panels meta_loading)
   AUTOSAVE_WAIT_SCREEN_HANDLERS
}

{new UIScreen autosave_wait_in_meta_screen  
   (panels meta_loading meta)
   AUTOSAVE_WAIT_SCREEN_HANDLERS
}

; for unloading the game before loading the metagame, don't go to this screen
; directly, go to one of the specialized transition screens below
; Handlers for the second meta_loading_screen
#define META_LOADING_TRANSITION_HANDLERS
(
   (transition_screen {object ""})
	(autosave FALSE)

   (delay_frames -1)

   (enter
      ; graphics resources aren't freed instantly, wait two frames after unload
      ; to avoid memory fragmentation
      {set [delay_frames] 2}
         
      {if {[transition_screen] get clear_game_state}
         ; We need to wipe the session and all other game state now
         {session clear {[transition_screen] get session_leaving_early}}
         {tour clear_performer}
         {gamecfg reset_config}
         
         {if {band_ui_sink get invite_accepted}
            ; need to set our new mode to that of the game we were invited to
            {gamemode set_mode {{session_searcher get_last_invite_result} get_mode_name}}
         }
      }
         
      ;; Now is a safe time to kick out any players who have disconnected
      {gamecfg set_active_roster FALSE}
		
		;{band_ui_sink check_controllers}
   )
   
   (poll
      {-- [delay_frames]}
      
      {if {== [delay_frames] 0}
         {set [delay_frames] -1}
         
         ; Reenable the UI
         {band_ui_sink set ui_disabled FALSE}
         
         ; Reset the band ui sink's block level
         {band_ui_sink set block_mode kBlockNone}
      
         ; autosave. for net games, put everyone into the "waiting" state
         {game foreach_local_player_config $pcfg
            {$pcfg set_ui_waiting TRUE}         
         }
         
			{if_else [autosave]
	         {do ($autosave_screen
	               {if_else {meta is_up}
	                  autosave_wait_in_meta_screen
	                  autosave_wait_screen
	               }
	            )
	             
	            {$autosave_screen set next_screen {[transition_screen] get next_screen}}
	            {autosave_push_goto $autosave_screen}
	         }
			; else
				{ui goto_screen {[transition_screen] get next_screen}} ; skip it
			}
      }
   )
)

; Our two loading screens differ only in if they have the meta panel loaded
{new UIScreen meta_loading_screen
   (panels meta_loading)
   META_LOADING_TRANSITION_HANDLERS
}

{new UIScreen meta_loading_in_meta_screen
   (panels meta_loading meta)
   META_LOADING_TRANSITION_HANDLERS   
}

; Meta loading screens, they represent a transition from game to metagame
; Unloads the game before starting to load the metagame
#define META_LOADING_SCREEN_HANDLERS
(   
   (panels meta_loading)
   (loading_screen '')
   (delay_frames -1)
      
   (enter
      {set [delay_frames] 3} ; three frames, so we draw before unloading world
      ; we choose the loading screen based on whether the meta panel is loaded
      {set [loading_screen]
         {if_else {meta is_up}
            meta_loading_in_meta_screen
            meta_loading_screen
         }
      }      
      {[loading_screen] set transition_screen $this}
		{[loading_screen] set autosave [autosave]}
		{band_ui_sink hide_error_dialog} ; hide error dialogs on meta loading
   )
   
   (poll
      {-- [delay_frames]}
      
      {if {== [delay_frames] 0}
         {set [delay_frames] -1}
         ; disable net sync
         {band_ui_sink set_net_sync FALSE}
      
         ; this transition is always allowed
         {band_ui_sink set ui_disabled FALSE}
         
         ; we use reset_screen because we might have pushed a screen
         ; force_reset_screen is a special BandUISink handler that gives TheUI
         ; an extra Poll, to avoid a frame of the game/world drawing beneath us
         {band_ui_sink force_reset_screen [loading_screen]}
         
         ; no more transitions till we reach loading_screen
         {band_ui_sink set ui_disabled TRUE}         
      } 
   )
)

; for quitting game and going to continue (song select or tour town) screen
{new UIScreen meta_loading_continue_screen
   META_LOADING_SCREEN_HANDLERS
   (clear_game_state FALSE)
   (session_leaving_early FALSE)
   (next_screen {gamemode get continue_screen})
	(autosave TRUE)
}

; for quitting any mode and going to main menu
{new UIScreen meta_loading_main_screen
   META_LOADING_SCREEN_HANDLERS
   (clear_game_state TRUE)
   (session_leaving_early FALSE)
   (next_screen {gamemode get main_screen})
	(autosave TRUE)
}

; for quitting any mode and going to pause_new_song_screen
{new UIScreen meta_loading_pause_new_section_screen
   META_LOADING_SCREEN_HANDLERS
   (clear_game_state FALSE)
   (session_leaving_early FALSE)
   (next_screen practice_sel_section_screen)
	(autosave FALSE)
}

#ifndef HX_XBOX
{new UIScreen meta_loading_pause_change_speed_screen
   META_LOADING_SCREEN_HANDLERS
   (clear_game_state FALSE)
   (session_leaving_early FALSE)
   (next_screen practice_sel_speed_screen)
	(autosave FALSE)
}
#endif

; for quitting any mode and going to pause_new_song_screen
{new UIScreen meta_loading_pause_new_song_screen
   META_LOADING_SCREEN_HANDLERS
   (clear_game_state FALSE)
   (session_leaving_early FALSE)
   (next_screen {gamemode get pause_new_song_screen})
	(autosave FALSE)
}

; for quitting any mode and going to pause_quit_screen
{new UIScreen meta_loading_quit_early_screen
   META_LOADING_SCREEN_HANDLERS
   (clear_game_state TRUE)
   (session_leaving_early TRUE)
   (next_screen main_screen)
	(autosave FALSE)
}

; for quitting and selecting new difficulty
{new UIScreen meta_loading_ready_screen
   META_LOADING_SCREEN_HANDLERS
   (clear_game_state FALSE)
   (session_leaving_early FALSE)
   (next_screen {gamemode get ready_screen})
	(autosave FALSE)
}

; for quitting and going to practice mode
{new UIScreen meta_loading_practice_screen
   META_LOADING_SCREEN_HANDLERS
   (clear_game_state FALSE)
   (session_leaving_early FALSE)
   (next_screen practice_sel_speed_screen)
   (autosave FALSE)
}

; for quitting and going to vocal practice mode
{new UIScreen meta_loading_practice_vocals_screen
   META_LOADING_SCREEN_HANDLERS
   (clear_game_state FALSE)
   (session_leaving_early FALSE)
   (next_screen practice_pre_sel_section_screen)
   (autosave FALSE)
}

; for starting the tour mode "win sequence"
{new UIScreen meta_loading_tour_win_screen
   META_LOADING_SCREEN_HANDLERS
   (clear_game_state FALSE)
   (session_leaving_early FALSE)
   (next_screen {gamemode get tour_win_screen})
	(autosave TRUE)
}

; for signin changes
{new UIScreen meta_loading_signout_screen
   META_LOADING_SCREEN_HANDLERS
   (clear_game_state TRUE)
   (session_leaving_early TRUE)
   (next_screen {gamemode get uisink_signout_screen})
	(autosave FALSE)
}

; for accepting invites in-game
{new UIScreen meta_loading_matchmaking_screen
   META_LOADING_SCREEN_HANDLERS
   (clear_game_state TRUE)
   (session_leaving_early TRUE)
   (next_screen {gamemode get matchmaking_screen})
	(autosave FALSE)
}

; for quitting ranked matches early
{new UIScreen meta_loading_matchmaking_forfeit_screen
   META_LOADING_SCREEN_HANDLERS
   (clear_game_state TRUE)
   (session_leaving_early TRUE)
   (next_screen {gamemode get matchmaking_screen})
	(autosave FALSE)
}

; for coming back to matchmaking without clearing band membership
{new UIScreen meta_loading_changemembers_screen
   META_LOADING_SCREEN_HANDLERS
   (clear_game_state FALSE)
   (session_leaving_early FALSE)
   (next_screen {gamemode get matchmaking_screen})
	(autosave FALSE)
}

{new UIScreen meta_loading_uisink_default_screen
   META_LOADING_SCREEN_HANDLERS
   (clear_game_state TRUE)
   (session_leaving_early FALSE)
   (next_screen {gamemode get uisink_default_screen})
	(autosave FALSE)
}

{new UIScreen meta_loading_uisink_lost_connection_screen
   META_LOADING_SCREEN_HANDLERS
   (clear_game_state TRUE)
   (session_leaving_early TRUE)
   (next_screen {gamemode get uisink_lost_connection_screen})
	(autosave FALSE)
}

{new UIScreen meta_loading_tutorial_screen
   META_LOADING_SCREEN_HANDLERS
   (clear_game_state FALSE)
   (session_leaving_early FALSE)
   (next_screen tutorials_lesson_menu_screen)
	(autosave FALSE)
}

{new UIScreen meta_loading_tutorial_main_screen
   META_LOADING_SCREEN_HANDLERS
   (clear_game_state FALSE)
   (session_leaving_early FALSE)
   (next_screen {gamemode get quit_screen})
	(autosave TRUE)
}

{new UIScreen meta_loading_featurette_history_screen
   META_LOADING_SCREEN_HANDLERS
   (clear_game_state FALSE)
   (session_leaving_early FALSE)
   (next_screen featurette_goto_history_screen)
	(autosave FALSE)
}

{new UIScreen meta_loading_featurette_music_screen
   META_LOADING_SCREEN_HANDLERS
   (clear_game_state FALSE)
   (session_leaving_early FALSE)
   (next_screen featurette_goto_music_screen)
	(autosave FALSE)
}

{new UIScreen meta_loading_featurette_peripherals_screen
   META_LOADING_SCREEN_HANDLERS
   (clear_game_state FALSE)
   (session_leaving_early FALSE)
   (next_screen featurette_goto_peripherals_screen)
	(autosave FALSE)
}

{new UIScreen meta_loading_featurette_style_screen
   META_LOADING_SCREEN_HANDLERS
   (clear_game_state FALSE)
   (session_leaving_early FALSE)
   (next_screen featurette_goto_style_screen)
	(autosave FALSE)
}
