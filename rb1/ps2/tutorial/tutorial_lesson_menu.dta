;;;
;;; generic menu for selecting a lesson for a specific instrument
;;;
{new UIPanel
   tutorials_lesson_menu_panel
   ;;
   ;; get the menu assets from the mode
   ;;
   (file {gamemode get lesson_menu_assets})

   ;;
   ;; goes to a lesson by number
   ;;
   (goto_lesson
      ($num)
      {tutorials_state_panel set_lesson $num}
      {ui goto_screen tutorials_loading_screen}
   )

   ;;
   ;; constructor
   ;;
   (enter
      ; save any completed tutorials
      {autosave}

      ; set the focus to the first lesson
      {$this set_focus
         {elem
            {tutorials_state_panel get_lesson
               {{gamemode get main_panel} get lesson_num}
            }
            0
         }
      }

      ; show checks next to all completed lessons
      {foreach $lesson {tutorials_state_panel get_all_lessons}
         {{elem $lesson 3} set_showing
            {{profile_mgr get_options_from_player {gamecfg get leader_num}} is_lesson_complete
               {elem $lesson 2}
            }
         }
      }
      {$this set just_entered FALSE}
   )

   ;;
   ;; destructor
   ;;
   (exit {$this set just_entered TRUE})

   ;;
   ;; handler for scrolling
   ;;
   (FOCUS_MSG
      {if {! [just_entered]}
         {do
            {set [lesson_num_scroll] 0}
            {foreach $lesson {tutorials_state_panel get_all_lessons}
               {if_else {== {elem $lesson 0} $new_focus}
                  {tutorials_state_panel remember_lesson [lesson_num_scroll]}
                  {++ [lesson_num_scroll]}
               }
            }
         }
      }
      kDataUnhandled
   )

   ;;
   ;; handler for pressing a button down
   ;;
   (SELECT_START_MSG
      ; the selecting controller will play the tutorial
      {{game get_participant_config 0} set_ready TRUE}
      kDataUnhandled
   )

   ;;
   ;; handler for selecting something
   ;; 
   (SELECT_MSG
      {set [lesson_num] 0}
      {foreach $lesson {tutorials_state_panel get_all_lessons}
         {if_else {== {elem $lesson 0} $component}
            {$this goto_lesson [lesson_num]}
            {++ [lesson_num]}
         }
      }
      kDataUnhandled
   )

   (allow_player
      ($player_num)
      {game is_player_participating $player_num}
   )

   ;;
   ;; variables
   ;;
   (lesson_num 0)
   (lesson_num_scroll 0)
   (just_entered TRUE)
}

;;;
;;; generic menu screen for selecting a tutorial lesson
;;; for a specific instrument
;;;
{new UIScreen
   tutorials_lesson_menu_screen
   (panels meta movie_panel tutorials_lesson_menu_panel helpbar)
   (focus tutorials_lesson_menu_panel)
   (back training_menu_screen)
   (helpbar (helpbar_back helpbar_select))

   (enter
      ; when we reach this screen, we're bound to a controller/instrument
      {band_ui_sink set post_matchmaking TRUE}
   )
}