;;;
;;; the tutorial fail panel
;;;
{new UIPanel tutorials_fail_panel
   ;;
   ;; properties
   ;;
#ifdef HX_XBOX
   (file "tutorial_fail.milo")
#else
   (file "tutorial_fail_og.milo")
#endif


   ;;
   ;; enter
   ;;
   (enter
      {$this set_focus tut_fail_retry.btn}
      {set [is_done] FALSE}
   )


   ;;
   ;; selecting something
   ;;
   (SELECT_MSG
      {if_else {&& {== $player_num {{gamemode get main_panel} get_tutorial_player}}
                   {! [is_done]}}
         {do
            {set [is_done] TRUE}
            {switch $component
               (tut_fail_retry.btn
                  {tutorials_state_panel set fail_menu_response "retry"}
                  HIDE_UI_ELEMENTS
               )
               (tut_fail_skip.btn
                  {tutorials_state_panel set fail_menu_response "skip"}
               )
            }
            {script_task kTaskUISeconds (delay 0.8) (script {ui pop_screen})}
            kDataUnhandled
         }
         TRUE
      }
   )


   ;;
   ;; pressing a button down
   ;;
   (BUTTON_DOWN_MSG
      {if_else {&& {== $player_num {{gamemode get main_panel} get_tutorial_player}}
                   {! [is_done]}}
         {do
            kDataUnhandled
         }
         TRUE
      }
   )


   ;;
   ;; releasing a button
   ;;
   (BUTTON_UP_MSG
      {if_else {&& {== $player_num {{gamemode get main_panel} get_tutorial_player}}
                   {! [is_done]}}
         {do
            kDataUnhandled
         }
         TRUE
      }
   )


   ;;
   ;; properties
   ;;
   (is_done FALSE)
}


;;;
;;; the tutorial fail screen
;;;
{new UIScreen tutorials_fail_screen
   (panels tutorials_fail_panel helpbar)
   (helpbar ('' helpbar_select))
   (focus tutorials_fail_panel)
   (enter)
   (exit
      {{gamemode get main_screen} set unpause_on_transition TRUE}
   )
}
