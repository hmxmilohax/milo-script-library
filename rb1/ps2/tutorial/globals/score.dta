;;;;
;;;; NOTE:
;;;;
;;;; These are for modifying the system of displaying the
;;;; player's score on tutorial tasks.
;;;;


;;;
;;; freeze/unfreeze the score
;;;
#define FREEZE_SCORE_TEXT
(
   {tutorials_state_panel set score_text_frozen TRUE}
)
#define UNFREEZE_SCORE_TEXT
(
   {tutorials_state_panel set score_text_frozen FALSE}
)


;;;
;;; hide the score
;;;
#define HIDE_SCORE
(
   {show_score_text FALSE}
   {tv_score.grp set_showing FALSE}
)


;;;
;;; show the score
;;;
#define SHOW_SCORE
(
   {show_score_text TRUE}
   {score.mnm set_frame 0}
   ;{tv_score.grp set_showing TRUE}
)


;;;
;;; animate the score
;;;
#define ANIMATE_SCORE
(
   {if {tutorials_state_panel can_update_score}
      {do
         {score_animate.tnm animate
            (units kTaskTutorialSeconds)
            (period 0.15)
            (range 0 2)}}}
)


;;;
;;; set the score graphic to a specific score out of 8
;;;
{func set_score_graphic ($score)
   {if {tutorials_state_panel can_update_score}
      {score.mnm set_frame $score}}
}


;;;
;;; update the score graphic with an arbitrary score out of total
;;;
{func update_score_graphic ($score $total)
   {set_score_graphic {int {* 8.0 {/ $score {* 1.0 $total}}}}}
}


;;;
;;; update the lesson's score
;;;
{func update_task_score ($label $score $total)
   {update_score_graphic $score $total}
   {set_score_text
      {sprintf
         {localize {gamemode get notes_format}}
         {- $total $score}}}
}
{func update_task_score_percent ($label $score $total)
   {update_score_graphic $score $total}
   {set_score_text
      {sprintf
         {localize {gamemode get percent_format}}
         {- 100 {int {* 100.0 {/ $score $total}}}}}}
}
{func zero_task_score_percent ($label)
   {update_task_score_percent $label 0 1}
}
