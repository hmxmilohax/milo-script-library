(RndDir
   (types
      (popup_help
         (editor
            (reset script (script {$this reset}))
            (hide script (script {$this help [last_key] FALSE hide}))
            ; help functions
            (kick script (script {$this help kick TRUE drum}))
            (deploy_guitar script (script {$this help deploy TRUE guitar}))
            (deploy_bass script (script {$this help deploy TRUE bass}))
            (deploy_vocals script (script {$this help deploy TRUE vocals}))
            (drum_fill script (script {$this help drum_fill TRUE drum}))
            (bre_guitar script (script {$this help rock_ending TRUE guitar}))
            (bre_drum script (script {$this help rock_ending TRUE drum}))
            (tambourine script (script {$this help tambourine TRUE vocal}))
            (tamb_note script (script {$this tambourine_note}))
            (dts_guitar script (script {$this help deploy_to_save TRUE guitar}))
            (dts_bass script (script {$this help deploy_to_save TRUE bass}))
            (dts_drum script (script {$this help deploy_to_save TRUE drum}))
            (dts_vocals script (script {$this help deploy_to_save TRUE vocals}))
         )
         
         ; remember the last state that was on or FALSE
         (last_key 'none')
         ; since this is on a poll, use an active flag so it only gets called once
         (active FALSE)

         ; remember if popup help should be happening at all or not
         (popup_help_disabled FALSE)   
         
         (help ($key $state $instrument)
            {if_else $state
               {do
                  {switch $key
                     (kick
                        {if {! [popup_help_disabled]}
                           {set [last_key] kick}
                           {$this kick}
                        }
                     )
                     (deploy
                        {if {! [popup_help_disabled]}
                           {if {== $instrument vocals}
                              {set [last_key] deploy}
                              {$this scream}
                           }
                           {if {|| {== $instrument guitar} {== $instrument bass}}
                              {set [last_key] deploy}
                              {$this tilt}
                           }
                        }
                     )
                     (drum_fill
                        {if {! [popup_help_disabled]}
                           {set [last_key] fill}
                           {$this drum_fill}
                        }
                     )
                     (rock_ending
                        {if {! [popup_help_disabled]}
                           {set [last_key] rock_ending}
                           {if_else {== $instrument drum}
                              {$this bre_drums}
                              {$this bre_guitar}
                           }
                        }
                     )
                     (tambourine
                        {if {! [popup_help_disabled]}
                           {set [last_key] tambourine}
                           {$this tambourine}
                        }
                     )
                     (deploy_to_save
                        {if {! [popup_help_disabled]}
                           {if {! [active]}
                              {set [last_key] deploy_to_save}
                              {switch $instrument
                                 (guitar {set $icon tilt})
                                 (bass {set $icon tilt})
                                 (drum {set $icon drum_deploy})
                                 (vocals {set $icon scream})
                              }
                              {{sprintf "%s.mnm" $icon} animate (loop 0 {{sprintf "%s.mnm" $icon} end_frame})}
                              {deploy_to_save.trig trigger}
                              {set [active] TRUE}
                           }
                        }
                     )
                  }
               }
               
               ; if state is false then check to see why it's on
               {do
                  {if {== $key deploy_to_save}
                     {set [active] FALSE}
                  }
                  {if {== $key [last_key]}
                     ; kill all thread tasks that might be associated with this
                     {do ($task_name)
                        {set $task_name {sprint $instrument "_deploy_help"}}
                        {if {exists $task_name} {delete $task_name}}
                     }  
                     {set [last_key] "none"}
                     {$this hide}
                  }
               }
            }
         )
                
         (reset 
            {$this help [last_key] FALSE drum}
            {$this help [last_key] FALSE guitar}
            {$this help [last_key] FALSE vocals}
            {$this help [last_key] FALSE bass}
            {reset.trig trigger}
         )
                
         (hide
            {hide.trig trigger}
         )
         
         (kick_note
            {if {== [last_key] kick}
               {kick.mnm animate}
            }
         )
                  
         ; for each popup help change the material
         (bre_drums
            {show.trig trigger}
            {drum_fill.mnm animate (loop 0 {drum_fill.mnm end_frame})}   
         )
         
         (bre_guitar
            {show.trig trigger}
            {strum.mnm animate (loop 0 {strum.mnm end_frame})}
         )
         
         (drum_fill
            {show.trig trigger}
            {drum_fill.mnm animate (loop 0 {drum_fill.mnm end_frame})}
         )
         
         (kick 
            {show.trig trigger}
            {kick.mnm animate (range 0 0)}
         )
         
         (tilt
            {show.trig trigger}
            {tilt.mnm animate (loop 0 {tilt.mnm end_frame})}
            {$this hide_delay 4}
         )
         
         (scream
            {show.trig trigger}
            {scream.mnm animate (loop 0 {scream.mnm end_frame})}   
         )
         
         (tambourine
            {show.trig trigger}
            {tambourine.mnm animate (range 0 0)}   
         )
         
         ; call whenver the user is supposed to hit tambourine
         (tambourine_note
            {if {== [last_key] tambourine}
               {tambourine.mnm animate}
            }
         )
         
         (hide_delay ($time)
            {script_task kTaskBeats (delay $time)
               (script {$this hide})
            }
         )
      )
   )
)