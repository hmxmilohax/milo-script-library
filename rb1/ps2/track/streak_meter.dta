(RndDir
   (types
      (streak_meter
         (editor
            (color_list (array color))
            (test script (script {$this test}))
            (break script (script {$this break}))
            (stop_test script (script {$this stop_test}))
            (star_deploy script (script {$this star_deploy}))
            (star_deploy_stop script (script {$this star_deploy_stop}))
         )
         
         ; list of colors to represent
         (color_list ())
         
         (streak_multiplier 1)
         (band_multiplier 1)
         (max_multiplier 4)
         
         (set_band_multiplier ($mult)
            {if {!= $mult [band_multiplier]}
               {set [band_multiplier] $mult}
               {$this multiplier_changed}
            }
         )
         
         (set_multiplier ($mult)
            {if {!= $mult [streak_multiplier]}
               {set [streak_multiplier] $mult}
               
               {if_else {>= $mult [max_multiplier]}
                  {$this start_super_streak}
                  {$this end_super_streak}
               }

               ; play sfx on building streak
               {if {> $mult 1}
                  {if {&& {exists gamemode} {gamemode get play_streak_sfx}}
                     {synth play streak_success.cue}}
               }

               {$this multiplier_changed}
            }
         )
         
         (multiplier_changed
            {do ($count {$this get_total_multiplier})
               {if_else {> $count 1}
                  {do
                     ; change the size of the streak multiplier background based on the text
                     {switch $count
                        (2 {multi-meter_anim.tnm animate (range 0 8)})
                        (5 {multi-meter_anim.tnm set_frame 8})
                        (6 {multi-meter_anim.tnm set_frame 8})
                        (10 {multi-meter_anim.tnm set_frame 10})
                        (12 {multi-meter_anim.tnm set_frame 9})
                     }
                     {multiplier.txt set_showing TRUE}
                     {multiplier.txt set_text {sprint $count "x"}}
                  }
                  {multiplier.txt set_showing FALSE}
               }
               
               ;{if {< [streak_multiplier] {size [color_list]}}   ; not the last one
                  ;{meter_wipe.mat set color {elem [color_list] [streak_multiplier]}}
                  ;{streak_backgrnd.mnm set_frame {- [streak_multiplier] 1}}
               ;}
               
               ; celebrate if the multiplier > 1
               {if {> $count 1}
                  {new_streak.trig trigger}
               }
            }
         )
         
         (set_hit ($hit_pct)                        
            {meter_wipe.mnm set_frame $hit_pct}
         )
         
         (break           
            {if {> [streak_multiplier] 1}
               ; if we had any lights on then call a special trigger
               {broken_glow.trig trigger}
            }
            {$this set_multiplier 1}
            {meter_wipe.mnm set_frame 0}
            {set [test_hit_val] 0}
         )
         
         (reset
            {$this set_multiplier 1}
            {reset.trig trigger}
         )
         
         (get_total_multiplier
            {if_else $edit_mode
               [streak_multiplier]
               {if_else {&& {== $game.play_mode coop} {game multiplayer}}
                  [streak_multiplier]
                  {* [streak_multiplier] [band_multiplier]}
               }
            }
         )
         
         (start_super_streak
            {show_blue.trig trigger}
         )
         
         (end_super_streak
            {hide_blue.trig trigger}   
         )
         
         ;;;;;;;;;;;;;;;;;; for testing in Milo ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
         (test_hit_val 0)
         
         (test
            {$this stop_test}
            {set [test_hit_val] 0}
            {thread_task kTaskSeconds (name streak_meter_test) 
               (script 
                  {set [test_hit_val] [test_hit_val]}
                  {if {> [test_hit_val] 1}
                     {set [test_hit_val] {- [test_hit_val] 1}}
                     {$this set_multiplier {+ [streak_multiplier] 1}}
                  }
                  {set [test_hit_val] {+ [test_hit_val] .05}}
                  {$this set_hit [test_hit_val]}
                  {$task sleep .1}
                  {$task loop}
               )
            }
         )
         
         (stop_test
            {if {exists streak_meter_test}
               {delete streak_meter_test}
            }
            {$this set_multiplier 1}
            {$this set_hit 0}
         )
         
         (star_deploy
            {star_deploy.trig trigger}
         )
         
         (star_deploy_stop
            {star_deploy_stop.trig trigger}
         )
         ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
      )
   )
)