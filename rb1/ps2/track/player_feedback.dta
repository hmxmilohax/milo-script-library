#define RESULTS (
   failed_solo
   bad_solo
   okay_solo
   solid_solo
   great_solo
   awesome_solo
   perfect_solo
   vox_lousy
   vox_poor
   vox_ok
   vox_good
   vox_great
)

(RndDir
   (types
      (player_feedback
         (editor
            (reset script (script {$this reset}))
            (player_name string (post_sync {$this setup_player [player_name] vocals}))
            (start_solo script (script {$this start_solo}))
            (end_solo script (script {$this end_solo}))
            (test_val int (post_sync {$this set_percent [test_val]}))
            (test_result
               symbol
               (list (RESULTS))
               (post_sync {$this set_result [test_result]})
            )
            (start_super_streak script (script {$this start_super_streak}))
            (end_super_streak script (script {$this end_super_streak}))
         )

         (player_name "")

         (test_val 0)
         (test_result vox_lousy)

         (in_solo FALSE)

         ; triggered when the solo starts
         (start_solo
            {reset.trig trigger}
            {solo_start.trig trigger}
            {$this set_percent 0}
            {set [in_solo] TRUE}
         )

         ; triggered when the solo ends
         (end_solo
            {if [in_solo]
               {solo_end.trig trigger}
               {set [in_solo] FALSE}
            }
         )

         (start_super_streak
            {reset.trig trigger}
            {super_streak_start.trig trigger}
            {script_task kTaskSeconds (delay 4.0) (script {$this end_super_streak})}
         )

         (end_super_streak {super_streak_end.trig trigger})

         ; reset
         (reset
            {reset.trig trigger}
            {set [in_solo] FALSE}
         )

         (set_result
            ($sym)
            {solo_rating.lbl set text_token $sym}
         )

         (set_award
            ($in_val)
            ; set the points text
            {score.txt set_text {fmt_score {int $in_val}}}
         )

         (set_percent
            ($in_val)
            {do
               ($val {int $in_val})
               ($string {sprintf "%d%%" $val})

               ; set the text of the text field
               {solo_percent.txt set_text $string}
               ; set the growth animation
               {percent_growth.tnm set_frame
                  {* {/ $val 100} {percent_growth.tnm end_frame}}
               }
               ; trigger the pop animation
               {pop.tnm animate}
            }
         )

         (setup_player
            ($name $instrument)
            {switch $instrument
               (bass {icon.lbl set text_token B})
               (drum {icon.lbl set text_token D})
               (guitar {icon.lbl set text_token G})
               (vocals {icon.lbl set text_token V})
            }

            {player_name.lbl set text_token $name}

            ; Manually center the labels
            {do
               ($icon_width {icon.lbl get_string_width {icon.lbl get text_token}})
               ($name_width
                  {player_name.lbl get_string_width {player_name.lbl get text_token}}
               )
               ($total_width {+ $icon_width $name_width})
               ($x)
               ($y)
               ($z)

               ;get the position of the icon
               {icon.lbl get_local_pos $x $y $z}
               {icon.lbl set_local_pos {/ {+ {- $total_width} $icon_width} 2} $y $z}
            }
         )
      )
   )
)