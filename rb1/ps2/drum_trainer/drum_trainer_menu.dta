;;;
;;; menu for selecting the drum trainer mode panel
;;;
{new SessionPanel drum_trainer_menu_panel
   (file "drum_trainer_menu.milo")
   (focus beats.btn)
   (enter
      {session clear}
   )
   (SELECT_START_MSG
      {switch $component
         (beats.btn
            {gamemode set_mode drum_trainer_beats $player_num}
         )
         (fills.btn
            {gamemode set_mode drum_trainer_fills $player_num}
         )
         (freestyle.btn
            {gamemode set_mode drum_trainer_freestyle $player_num}
         )
      }
      {$this wait_for_session_ready}
      {session_mgr add_local_player
         $player_num
         {gamecfg
            get_controller_type_from_controller
            $player_num
         }
      }
      kDataUnhandled
   )
   (session_ready
      {gamecfg auto_assign_missing_slots}
      {do ($player_num {gamemode get player_num})
         {set_char_from_controller $player_num}         
         {game set_song {gamemode get start_song}}
         {game set_venue arena_01}
         {do ($player_config {game get_player_config $player_num})
            {$player_config set_difficulty {gamemode get start_difficulty}}
            {$player_config set_track_type {gamemode get start_tracktype}}
            {$player_config set_ready TRUE}
         }
         {{gamemode get game_panel} set player_num $player_num}
      }
      {ui goto_screen loading_screen}
   )
}


;;;
;;; menu for selecting the drum trainer mode screen
;;;
{new UIScreen drum_trainer_menu_screen
   (panels meta movie_panel drum_trainer_menu_panel)
   (focus  drum_trainer_menu_panel)
   (back   training_menu_screen)
   (helpbar (helpbar_back helpbar_select))
}
