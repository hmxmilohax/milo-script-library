{new UIPanel
   lose_retail_demo_splash_panel
   (file "retail_demo_splash.milo")
   (next_screen meta_loading_retail_demo_timeout_screen)
   (do_the_net_sync TRUE)
   (already_going TRUE)
   (on_continue
      {set [already_going] TRUE}
      {if [do_the_net_sync]
         {band_ui_sink set_net_sync TRUE}
      }
      {ui goto_screen [next_screen]}
   )
   (on_timeout
      {if
         {&&
            {! [already_going]}
            {== {ui current_screen} lose_retail_demo_splash_screen}
         }
         {$this on_continue}
      }
   )
   (SELECT_START_MSG {$this on_continue})
   (enter
      {helpbar set_dont_limit_to_session TRUE}
      {helpbar set_limit_to_player_num -1}
      {set [already_going] FALSE}
      {game foreach_player_config $pcfg
         {$pcfg set_lefty_flip_off}
      }
      {profile_mgr set_all_lefty_flip_off}
      {script_task kTaskSeconds
         (delay 20.0)
         (script {lose_retail_demo_splash_panel on_timeout})
      }
   )
   (exit {helpbar set_dont_limit_to_session FALSE})
}
{new UIScreen
   lose_retail_demo_splash_screen
   (panels lose_retail_demo_splash_panel helpbar)
   (helpbar ('' helpbar_continue))
   (focus lose_retail_demo_splash_panel)
}
#define LOSE_PANEL_HANDLERS (
   LIMIT_TO_LEADER_SESSION
   (quit_button '')
   (enter
      {$this set_focus lose_restart.btn}
      {lose_song_data.lbl set_localized {song_mgr song_name {game get_song}}}
      {lose_progress_data.lbl set_localized
         {sprintf
            {localize percent_complete_format}
            {{beatmatch active_player 0} percent_complete}
         }
      }
      {if_else {! {gamemode get is_solo}}
         {do
            {lose_quit.btn set_showing FALSE}
            {$this disable lose_quit.btn}
            {$this enable menu_item_3.btn}
            {set [quit_button] menu_item_3.btn}
            {[quit_button] set text_token QUIT}
         }
         {do
            {set [quit_button] lose_quit.btn}
            {$this enable [quit_button]}
            {[quit_button] set_showing TRUE}
            {[quit_button] set text_token QUIT}
            {if {exists menu_item_3.btn}
               {$this disable menu_item_3.btn}
               {menu_item_3.btn set text_token practice}
            }
         }
      }
      {session add_sink $this remote_player_left_msg}
      {$this set_up_buttons}
   )
   (exit {session remove_sink $this remote_player_left_msg})
   (set_up_buttons
      {if_else {< {session num_players} {gamemode get min_players}}
         {do
            {$this disable lose_restart.btn}
            {if {exists lose_diff.btn}
               {$this disable lose_diff.btn}
            }
            {$this disable lose_selsong.btn}
            {$this set_focus [quit_button]}
         }
         {do
            {$this enable lose_restart.btn}
            {if {exists lose_diff.btn}
               {$this enable lose_diff.btn}
            }
            {$this enable lose_selsong.btn}
         }
      }
      {$this disable lose_diff.btn}
      {$this disable lose_selsong.btn}
      {$this enable [quit_button]}
      {$this set_focus [quit_button]}
   )
   (REMOTE_PLAYER_LEFT_MSG {$this set_up_buttons})
   (SELECT_MSG
      {if_else {== $component lose_restart.btn}
         {do
            {session send_to_all (game_restart) kNetReliable}
            {game_restart}
         }
         {do
            {lose_retail_demo_splash_panel set
               next_screen
               meta_loading_retail_demo_timeout_screen
            }
            {lose_retail_demo_splash_panel set do_the_net_sync FALSE}
            {band_ui_sink sync_screen lose_retail_demo_splash_screen 1}
         }
      }
   )
   (confirm_goto
      ($destination)
      {band_ui_sink set_net_sync FALSE}
      {set $lose_confirm_screen_return_to {{ui current_screen} name}}
      {[quit_confirm_screen] set confirm_screen $destination}
      {ui goto_screen [quit_confirm_screen]}
   )
)
{new UIPanel
   lose
   (file lose.milo)
   (quit_confirm_screen lose_confirm_quit_screen)
   LOSE_PANEL_HANDLERS
}
{new UIPanel
   tour_solo_lose_panel
   (file tour_solo_lose.milo)
   (quit_confirm_screen lose_tour_solo_confirm_quit_screen)
   LOSE_PANEL_HANDLERS
}
#define LOSE_SCREEN_HANDLERS (
   (helpbar
      {if_else {is_leader_local}
         ('' helpbar_select)
         (helpbar_back '')
      }
   )
   HELPBAR_WAITING_FOR_LEADER
)
{new UIScreen
   lose_screen
   (panels lose helpbar)
   (focus lose)
   LOSE_SCREEN_HANDLERS
}
{new UIScreen
   tour_solo_lose_screen
   (panels tour_solo_lose_panel helpbar)
   (focus tour_solo_lose_panel)
   LOSE_SCREEN_HANDLERS
}
{new UIScreen
   goto_practice_screen
   (panels)
   (TRANSITION_COMPLETE_MSG
      {gamemode set_mode practice}
      {band_ui_sink sync_screen meta_loading_practice_screen 1}
   )
}
#define COMMON_LOSE_QUIT_CONFIRM (
   (focus dialog_panel)
   (confirm_screen '')
   (quitting FALSE)
   (back $lose_confirm_screen_return_to)
   (enter
      {dialog_panel set_yesno {localize confirm_quit} no.btn}
      {dialog_panel set limit leader_session}
      {$this set_up_buttons}
      {session add_sink $this remote_player_left_msg}
   )
   (exit
      {session remove_sink $this remote_player_left_msg}
      #ifndef BAND_RETAIL_360
      {if {! quitting}
         {band_ui_sink set_net_sync TRUE}
      }
      #else
      {if {! {== [confirm_screen] lose_retail_demo_splash_screen}}
         {band_ui_sink set_net_sync TRUE}
      }
      #endif
   )
   (REMOTE_PLAYER_LEFT_MSG {$this set_up_buttons})
   QUIT_DIALOG_SET_UP_BUTTONS
   (SELECT_MSG
      {if_else
         {'||'
            {&& {is_leader_local} {game is_player_participating $player_num}}
            {gamemode get always_allow_player}
         }
         {switch $component
            (yes.btn
               {if {! {== [confirm_screen] meta_loading_ready_screen}}
                  {lose_retail_demo_splash_panel set next_screen [confirm_screen]}
                  {lose_retail_demo_splash_panel set do_the_net_sync {! [quitting]}}
                  {set [confirm_screen] lose_retail_demo_splash_screen}
               }
               {band_ui_sink sync_screen [confirm_screen] 1}
            )
            (no.btn
               {ui goto_screen $lose_confirm_screen_return_to}
               {band_ui_sink set_net_sync TRUE}
            )
         }
         kDataUnhandled
      }
   )
)
{new UIScreen
   lose_confirm_quit_screen
   (panels lose dialog_panel)
   COMMON_LOSE_QUIT_CONFIRM
}
{new UIScreen
   lose_tour_solo_confirm_quit_screen
   (panels tour_solo_lose_panel dialog_panel)
   COMMON_LOSE_QUIT_CONFIRM
}