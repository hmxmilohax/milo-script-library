{new PatchPanel
   patch_panel
   (file "patch.milo")
   (bg_color {pack_color 1 1 1})
   (exit_screen 0)
   (mode modify_menu)
   (mode_cache modify_menu)
   (show_modify_menu FALSE)
   (show_color FALSE)
   (show_category FALSE)
   (show_sticker FALSE)
   (show_layer FALSE)
   (show_nav FALSE)
   (show_layer_menu FALSE)
   (show_quit_menu FALSE)
   (layer_first FALSE)
   (edit_layer_idx_cache 0)
   (size_x 1)
   (size_y 1)
   (preview_frame 0)
   (help_shown 0)
   (patch_player_num -1)
   (set_size
      ($x $y $f)
      {set [size_x] $x}
      {set [size_y] $y}
      {set [preview_frame] $f}
   )
   (on_set_mode
      {set [show_color] FALSE}
      {set [show_modify_menu] FALSE}
      {set [show_sticker] FALSE}
      {set [show_layer] FALSE}
      {set [show_nav] FALSE}
      {set [show_category] FALSE}
      {set [layer_first] FALSE}
      {set [show_layer_menu] FALSE}
      {set [show_quit_menu] FALSE}
      {helpbar set_config (Back Menu)}
      {unless {'||' {== [mode] category} {== [mode] sticker}}
         {$this store_undo}
      }
      {switch [mode]
         (modify_menu
            {layer-modify.lst set_selected {layer.lst selected_pos}}
            {dpad.grp set_showing FALSE}
            {$this set_focus move.btn}
            {set [show_modify_menu] TRUE}
            {color.btn set_state
               {if_else {{$this edit_layer} allow_color}
                  kNormal
                  kDisabled
               }
            }
            {set [show_nav] TRUE}
            {helpbar set_config (helpbar_back helpbar_modifysticker)}
         )
         (quit
            {dpad.grp set_showing FALSE}
            {quit.grp set_showing TRUE}
            {$this set_focus cnf_save.btn}
            {set [show_quit_menu] TRUE}
            {set [show_nav] TRUE}
            {helpbar set_config ('' helpbar_select)}
         )
         (move
            {dpad.grp set_showing TRUE}
            {help_editing.lbl set text_token 'Move Sticker'}
            {helpbar set_config (helpbar_cancel helpbar_confirm)}
         )
         (flip
            {dpad.grp set_showing TRUE}
            {help_editing.lbl set text_token 'Flip Sticker'}
            {helpbar set_config (helpbar_cancel helpbar_confirm)}
         )
         (rotate
            {dpad.grp set_showing TRUE}
            {help_editing.lbl set text_token 'Rotate Sticker'}
            {helpbar set_config (helpbar_cancel helpbar_confirm)}
         )
         (scale
            {dpad.grp set_showing TRUE}
            {help_editing.lbl set text_token 'Scale Sticker'}
            {helpbar set_config (helpbar_cancel helpbar_confirm)}
         )
         (warp
            {dpad.grp set_showing TRUE}
            {help_editing.lbl set text_token 'Warp Sticker'}
            {helpbar set_config (helpbar_cancel helpbar_confirm)}
         )
         (color
            {dpad.grp set_showing FALSE}
            {$this set_focus color.lst}
            {set [show_color] TRUE}
            {color.lst set_selected {{$this edit_layer} get color_idx}}
            {helpbar set_config (helpbar_cancel helpbar_confirm)}
         )
         (category
            {helpbar set_config (Back Select)}
            {dpad.grp set_showing FALSE}
            {$this set_focus category.lst}
            {set [show_category] TRUE}
            {set [show_nav] TRUE}
            {$this set_focus category.lst}
            {helpbar set_config (helpbar_back helpbar_choose)}
         )
         (sticker
            {helpbar set_config (Back Select)}
            {sticker.lst set_provider
               {$this sticker_provider {category.lst selected_pos}}
            }
            {dpad.grp set_showing FALSE}
            {set [show_sticker] TRUE}
            {set [show_nav] TRUE}
            {$this set_focus sticker.lst}
            {helpbar set_config (helpbar_back helpbar_choose)}
         )
         (layer
            {if {!= [mode] [mode_cache]}
               {set [edit_layer_idx_cache] {layer.lst selected_pos}}
            }
            {layer.lst set_dirty}
            {set [show_layer] TRUE}
            {set [show_nav] TRUE}
            {$this set_focus layer.lst}
            {helpbar set_config (helpbar_back helpbar_choose)}
            {dpad.grp set_showing FALSE}
         )
         (layer_menu
            {layer-menu.lst set_selected {layer.lst selected_pos}}
            {set [show_nav] TRUE}
            {dpad.grp set_showing FALSE}
            {set [show_layer_menu] TRUE}
            {$this set_focus layer_edit.btn}
            {helpbar set_config (helpbar_back helpbar_choose)}
         )
         (layer_move
            {layer.lst set_dirty}
            {set [show_layer] TRUE}
            {set [show_nav] TRUE}
            {$this set_focus layer.lst}
            {helpbar set_config (Back Confirm)}
            {dpad.grp set_showing FALSE}
            {helpbar set_config (helpbar_cancel helpbar_reorderlayer)}
         )
      }
      {if_else [show_modify_menu]
         {modify_menu.grp set_showing TRUE}
         {modify_menu.grp set_showing FALSE}
      }
      {if_else [show_color]
         {color.grp set_showing TRUE}
         {color.grp set_showing FALSE}
      }
      {if_else [show_nav]
         {do
            {layers.grp set_showing [show_layer]}
            {layers_menu.grp set_showing [show_layer_menu]}
            {category.grp set_showing [show_category]}
            {stickers.grp set_showing [show_sticker]}
         }
         {do
            {layers.grp set_showing FALSE}
            {layers_menu.grp set_showing FALSE}
            {category.grp set_showing FALSE}
            {stickers.grp set_showing FALSE}
         }
      }
      {set [mode_cache] [mode]}
   )
   (enter
      {switch {rnd aspect}
         (kWidescreenAspect
            {aspect.tnm set_frame 10}
            {aspectright.tnm set_frame 10}
         )
         {aspect.tnm set_frame 0}
         {aspectright.tnm set_frame 0}
      }
      {helpbar set_mode_right}
      {preview.grp set_frame [preview_frame]}
      {arm_layout.grp set_frame [preview_frame]}
      {canvas.mat set color [bg_color]}
      {color.grp set_showing FALSE}
      {modify_menu.grp set_showing FALSE}
      {stickers.grp set_showing FALSE}
      {layers.grp set_showing FALSE}
      {quit.grp set_showing FALSE}
      {category.lst set_provider {$this category_provider}}
      {do
         ($layer {$this edit_layer})
         {color.lst set color_palette {$layer color_palette}}
         {color.lst set_selected {$layer get color_idx}}
      }
      {layer.lst set_provider {$this layer_provider}}
      {layer-modify.lst set_provider {$this layer_provider}}
      {layer-menu.lst set_provider {$this layer_provider}}
      {$this set edit_layer_idx {layer.lst selected_pos}}
      {$this set mode layer}
      {$this cache_patch}
   )
   (unload {helpbar set_mode_left})
   (SELECT_START_MSG
      {switch $component
         (layer.btn {$this set mode layer})
         (move.btn {$this set mode move})
         (color.btn {$this set mode color})
         (rotate.btn {$this set mode rotate})
         (sticker.lst {$this set mode layer})
         (warp.btn {$this set mode warp})
         (scale.btn {$this set mode scale})
         (done.btn {$this set mode layer_menu})
         (color.lst {$this set mode modify_menu})
         (layer_edit.btn {$this set mode modify_menu})
         (layer_sticker.btn {$this set mode category})
         (layer_move.btn {$this set mode layer_move})
         (layer_clear.btn
            {$this clear_layer {layer.lst selected_pos}}
            {layer.lst set_dirty}
            {$this set mode layer}
         )
         (layer_copy.btn
            {$this dupe_layer {layer.lst selected_pos}}
            {layer.lst set_dirty}
            {$this set mode layer}
         )
         (layer_done.btn {$this set mode quit})
         (category.lst
            {{$this edit_layer} set sticker_category {category.lst selected_sym}}
            {$this set mode sticker}
            {{$this edit_layer} set_scale [size_x] [size_y]}
            {{$this edit_layer} set_default_color}
         )
         (layer.lst {$this set mode category})
         (flip.btn {$this set mode flip})
         (cnf_discard.btn
            {$this restore_patch}
            {ui goto_screen [exit_screen]}
         )
         (cnf_save.btn {[exit_screen] confirm})
         (cnf_return.btn
            {preview.grp set_showing TRUE}
            {quit.grp set_showing FALSE}
            {$this set mode layer}
         )
      }
   )
   (SCROLL_MSG
      {switch [mode]
         (layer
            {$this set edit_layer_idx {layer.lst selected_pos}}
            {{$this edit_layer} select_fx}
            {$this set mode layer}
         )
         (sticker
            {{$this edit_layer} set sticker_idx {sticker.lst selected_pos}}
            {{$this edit_layer} set_default_color}
         )
         {switch $component
            (color.lst {{$this edit_layer} set color_idx {$component selected_pos}})
         }
      }
   )
   (BUTTON_DOWN_MSG
      {switch [mode]
         (modify_menu
            {if_else {== $button kPad_Tri}
               {$this set mode layer_menu}
               kDataUnhandled
            }
         )
         (quit kDataUnhandled)
         (color
            {switch $button
               (kPad_Tri
                  {$this restore_undo}
                  {synth play button_back}
                  {$this set mode modify_menu}
               )
               (kPad_X
                  {$this set mode modify_menu}
                  {synth play button_select}
               )
               kDataUnhandled
            }
         )
         (category
            {switch $button
               (kPad_Tri
                  {$this restore_undo}
                  {synth play button_back}
                  {$this set mode layer}
               )
               kDataUnhandled
            }
         )
         (layer_menu
            {if_else {== $button kPad_Tri}
               {$this set mode layer}
               kDataUnhandled
            }
         )
         (layer
            {switch $button
               (kPad_X
                  {if_else {{$this edit_layer} has_sticker}
                     {$this set mode layer_menu}
                     {$this set mode category}
                  }
               )
               (kPad_Tri {$this set mode quit})
               (kPad_DUp {layer.lst scroll 1 1})
               (kPad_DDown {layer.lst scroll -1 1})
               kDataUnhandled
            }
         )
         (flip
            {switch $button
               (kPad_DUp
                  {synth play button_select}
                  {{$this edit_layer} flip_y}
               )
               (kPad_DDown
                  {synth play button_select}
                  {{$this edit_layer} flip_y}
               )
               (kPad_DLeft
                  {synth play button_select}
                  {{$this edit_layer} flip_x}
               )
               (kPad_DRight
                  {synth play button_select}
                  {{$this edit_layer} flip_x}
               )
               (kPad_Tri
                  {$this restore_undo}
                  {synth play button_back}
                  {$this set mode modify_menu}
               )
               (kPad_Xbox_A {$this set mode modify_menu})
               kDataUnhandled
            }
         )
         (layer_move
            {switch $button
               (kPad_X
                  {synth play button_select}
                  {$this set edit_layer_idx {layer.lst selected_pos}}
                  {{$this edit_layer} select_fx}
                  {$this set mode layer_menu}
               )
               (kPad_Tri
                  {synth play button_back}
                  {$this set mode layer_menu}
               )
               (kPad_DDown
                  {if {$this swap_layers {layer.lst selected_pos} {- {layer.lst selected_pos} 1}}
                     {layer.lst scroll -1 [patch_player_num]}
                     {layer.lst set_dirty}
                  }
               )
               (kPad_DUp
                  {if
                     {$this swap_layers
                        {layer.lst selected_pos}
                        {'+' {layer.lst selected_pos} 1}
                     }
                     {layer.lst scroll 1 [patch_player_num]}
                     {layer.lst set_dirty}
                  }
               )
            }
         )
         (sticker
            {switch $button
               (kPad_Tri
                  {$this restore_undo}
                  {synth play button_back}
                  {$this set mode category}
               )
               (kPad_X {$this set mode layer_menu})
               kDataUnhandled
            }
         )
         {switch $button
            (kPad_Xbox_A
               {$this reset_velocities}
               {$this reset_directions}
               {synth play button_select}
               {if_else {{$this edit_layer} has_sticker}
                  {do
                     {set [show_modify_menu] TRUE}
                     {set [mode] modify_menu}
                  }
                  {set [mode] layer}
               }
            )
            (kPad_Xbox_B
               {$this restore_undo}
               {synth play button_back}
               {$this set mode modify_menu}
            )
         }
      }
   )
}
{new UIScreen shop_patch_screen (panels patch_panel) (focus patch_panel)}
{new UIScreen
   bandlogo_patch_screen
   (panels meta practice_space_panel patch_panel helpbar)
   (focus patch_panel)
   (show_char TRUE)
   (exit {patch_panel restore_patch})
   (unload {practice_space_panel refresh})
}
{new UIPanel
   patch_help_panel
   (file "patch_help.milo")
   (focus continue.btn)
   (enter
      {switch {rnd aspect}
         (kWidescreenAspect {aspect.tnm set_frame 10})
         {aspect.tnm set_frame 0}
      }
      {patch_panel set help_shown TRUE}
   )
}
#define PATCH_HELP_SCREEN_HANDLERS (
   (focus patch_help_panel)
   (helpbar ('' helpbar_continue))
   (BUTTON_DOWN_MSG
      {if {== $button kPad_X}
         {synth play button_select}
         {patch_panel set help_shown TRUE}
         {ui goto_screen [patch_screen]}
      }
   )
)
{new UIScreen
   shop_patch_help_screen
   (panels patch_help_panel)
   (patch_screen shop_patch_screen)
   PATCH_HELP_SCREEN_HANDLERS
}
{new UIScreen
   bandlogo_help_screen
   (panels meta practice_space_panel patch_help_panel helpbar)
   (patch_screen bandlogo_patch_screen)
   (show_char TRUE)
   PATCH_HELP_SCREEN_HANDLERS
}