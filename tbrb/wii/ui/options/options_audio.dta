{new UIPanel mixer_panel (file "mixer_parent.milo")}
{new UIPanel
   options_audio_panel
   #ifdef HX_PS3
   (file "options_audio_ps3_keep.milo")
   #else
   (file "options_audio.milo")
   #endif
   (focus background_music.btn)
   (load
      {content_mgr start_refresh}
      {content_loading_panel allowed_to_show TRUE}
   )
   (finish_load {content_loading_panel allowed_to_show FALSE})
   (is_loaded {content_mgr refresh_done})
   (enter
      {meta music_stop}
      {platform_mgr disable_xmp}
      {song_preview audio_sliders 0 0 FALSE}
      {$this update_all_from_profile}
      {song_preview_music {profile_mgr get_music_volume_db}}
      {song_preview_crowd {profile_mgr get_crowd_volume_db}}
      {memcardmgr add_sink $this (save_load_all_complete_msg)}
   )
   (exit
      {song_preview ''}
      {meta music_start}
      {memcardmgr remove_sink $this save_load_all_complete_msg}
   )
   (save_load_all_complete_msg {$this update_all_from_profile})
   (TRANSITION_COMPLETE_MSG
      {$this update_helpbar {$this focus_name}}
      kDataUnhandled
   )
   (FOCUS_MSG {$this update_helpbar $new_focus})
   (SCROLL_SELECT_MSG {$this on_slider_change $component})
   (SCROLL_MSG {$this on_slider_change $component})
   (SELECT_MSG
      {switch $component
         (karaoke.btn
            {do
               ($was_on {profile_mgr get_karaoke_mode})
               {profile_mgr set_karaoke_mode {! $was_on}}
               {$this update_from_profile check.mesh}
            }
         )
         (background_music.btn
            {background.sld store}
            {$this set_focus background.sld}
         )
         (crowd.btn
            {crowd.sld store}
            {$this set_focus crowd.sld}
         )
         (sfx.btn
            {sfx.sld store}
            {$this set_focus sfx.sld}
         )
      }
   )
   (BUTTON_DOWN_MSG
      {switch {$this focus_name}
         (background.sld
            {switch $action
               (kAction_Confirm
                  {background.sld confirm}
                  {play_instr_sfx $user button_select}
                  {$this set_focus background_music.btn}
               )
               (kAction_Cancel
                  {background.sld undo $user}
                  {$this set_focus background_music.btn}
               )
               kDataUnhandled
            }
         )
         (crowd.sld
            {switch $action
               (kAction_Confirm
                  {crowd.sld confirm}
                  {play_instr_sfx $user button_select}
                  {$this set_focus crowd.btn}
               )
               (kAction_Cancel
                  {crowd.sld undo $user}
                  {$this set_focus crowd.btn}
               )
               kDataUnhandled
            }
         )
         (sfx.sld
            {switch $action
               (kAction_Confirm
                  {sfx.sld confirm}
                  {play_instr_sfx $user button_select}
                  {$this set_focus sfx.btn}
               )
               (kAction_Cancel
                  {sfx.sld undo $user}
                  {$this set_focus sfx.btn}
               )
               kDataUnhandled
            }
         )
         kDataUnhandled
      }
   )
   (on_slider_change
      ($component)
      {switch $component
         (background.sld
            {profile_mgr set_music_volume {$component current}}
            {song_preview_music {profile_mgr get_music_volume_db}}
         )
         (crowd.sld
            {profile_mgr set_crowd_volume {$component current}}
            {song_preview_crowd {profile_mgr get_crowd_volume_db}}
         )
         (sfx.sld
            {profile_mgr set_fx_volume {$component current}}
            {play_game_seq slider_sfx.cue}
         )
      }
   )
   (update_all_from_profile
      {$this iterate
         UISlider
         $s
         {$s set_num_steps 12}
         {$this update_from_profile $s}
      }
      {$this update_from_profile check.mesh}
   )
   (update_from_profile
      ($component)
      {switch $component
         (check.mesh {check.mesh set_showing {profile_mgr get_karaoke_mode}})
         (background.sld {$component set_current {profile_mgr get_music_volume}})
         (crowd.sld {$component set_current {profile_mgr get_crowd_volume}})
         (sfx.sld {$component set_current {profile_mgr get_fx_volume}})
      }
   )
   (update_helpbar
      ($new_focus)
      {switch $new_focus
         (karaoke.btn
            {helpbar set_config
               (
                  (cancel helpbar_back)
                  (confirm helpbar_toggleoption)
               )
            }
         )
         (
            (background_music.btn crowd.btn sfx.btn)
            {helpbar set_config
               (
                  (cancel helpbar_back)
                  (confirm helpbar_changesetting)
               )
            }
         )
         (
            (background.sld crowd.sld sfx.sld)
            {helpbar set_config
               (
                  (cancel helpbar_cancel)
                  (confirm helpbar_confirmsetting)
               )
            }
         )
      }
   )
}
{new AutoloadScreen
   options_audio_screen
   (panels meta background_panel main_popup_bg_panel options_audio_panel)
   (focus options_audio_panel)
   (back options_console_screen)
   (helpbar
      (
         (cancel helpbar_back)
         (confirm helpbar_select)
      )
   )
}