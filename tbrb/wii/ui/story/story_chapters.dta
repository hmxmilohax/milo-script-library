{new StoryMusicPanel
   story_music
   #ifdef HX_WII
   (allow_home_menu TRUE)
   #endif
   (enter
      {platform_mgr enable_xmp}
      {meta music_stop}
      {$this music_start}
      {{story performer} check_for_fabfour}
   )
   (exit {$this music_stop})
   (start_ambient_sound
      {$this music_stop}
      {synth play media_screen.cue}
   )
   (stop_ambient_sound
      {synth stop media_screen.cue}
      {$this music_start}
   )
   (stop_all_sound
      {synth stop media_screen.cue}
      {$this music_stop}
   )
   (music_start
      {if
         {&&
            {$this is_up}
            {! {postsong_sfx_panel get active}}
            {! $mute_shell_music}
         }
         {{$this story_music} start}
      }
   )
   (music_stop {{$this story_music} stop})
}
{new UIPanel
   story_hub_panel
   (file "story_hub.milo")
   #ifdef HX_WII
   (allow_home_menu TRUE)
   #endif
   (focus "story.btn")
   (should_reset_focus TRUE)
   (BUTTON_DOWN_MSG
      {switch $action
         (kAction_Cancel {$this set should_reset_focus TRUE} kDataUnhandled)
         kDataUnhandled
      }
   )
   (SELECT_MSG
      {switch $component
         (story.btn
            {{story performer} select_most_recent_chapter}
            {ui goto_screen {get_story_destination_screen story_chapter_screen}}
         )
         (prizes.btn
            {if_else {session is_local}
               {ui goto_screen story_prizes_screen}
               {$this warn_unavailable story_prizes_unavailable}
            }
         )
         (memorabilia.btn
            {if_else {session is_local}
               {ui goto_screen story_memorabilia_screen}
               {$this warn_unavailable story_memorabilia_unavailable}
            }
         )
         (chapterchallenges.btn
            {if_else {{story progress} are_chapter_challenges_available}
               {ui goto_screen story_chapter_challenges_screen}
               {$this warn_unavailable story_challenges_unavailable}
            }
         )
         (accomplishments.btn
            {if_else {session is_local}
               {view_accomplishments_screen
                  $user
                  0
                  story_accomplishments_choose_user_screen
                  story_accomplishments_no_user_screen
                  story_accomplishments_screen
                  story_hub_screen
               }
               {$this warn_unavailable story_accomplishments_unavailable}
            }
         )
      }
   )
   (warn_unavailable
      ($message)
      {net_sync disable}
      {story_unavailable_screen set message $message}
      {ui push_screen story_unavailable_screen}
   )
   (enter
      {input_mgr set_limit kLimitSessionLeader}
      #ifndef HX_WII
      {presence_mgr set_in_story_shell}
      #endif
      {story_prizes_panel clear_saved_prize_position}
      {net_sync enable}
      {if [should_reset_focus]
         {$this set_focus "story.btn"}
      }
      {$this set should_reset_focus FALSE}
      {{story progress} clear_last_set_state}
      {if {is_leader_local}
         {{story performer} clear_song_set}
      }
      {chapterchallenges_new.lbl set_showing
         {{story progress} is_chapterchallenge_group_new}
      }
      {if_else {session is_local}
         {do
            {memo_new.lbl set_showing {{story progress} is_memo_group_new}}
            {prize_new.lbl set_showing {{story progress} is_prize_group_new}}
         }
         {do
            {memo_new.lbl set_showing FALSE}
            {prize_new.lbl set_showing FALSE}
         }
      }
   )
}
{new BandScreen
   story_accomplishments_screen
   (panels
      meta
      story_music
      background_panel
      story_hub_panel
      accomplishment_panel
   )
   (focus accomplishment_panel)
   (helpbar ((cancel helpbar_back)))
   (back story_hub_screen)
}
{new UIPanel
   story_accomplishments_choose_user_panel
   ACCOMPLISHMENTS_CHOOSE_USER_PANEL
   (on_go_to_accomplishments_screen)
}
{new BandScreen
   story_accomplishments_choose_user_screen
   (panels
      meta
      background_panel
      story_hub_panel
      story_accomplishments_choose_user_panel
   )
   (focus story_accomplishments_choose_user_panel)
   (helpbar
      (
         (cancel helpbar_back)
         (confirm helpbar_confirm)
      )
   )
}
{new BandScreen
   story_accomplishments_no_user_screen
   (panels meta background_panel story_hub_panel dialog_panel)
   (focus dialog_panel)
   (helpbar ((confirm helpbar_confirm)))
   (enter {dialog_panel set_ok accomplishments_need_signed_in_user})
   (SELECT_MSG {ui goto_screen story_hub_screen})
}
{new BandScreen
   story_unavailable_screen
   (panels dialog_panel)
   (focus dialog_panel)
   (message '')
   (enter
      {dialog_panel set_custom [message] "" return opt2.btn}
      {dialog_panel disable {dialog_panel find opt1.btn}}
   )
   (exit {dialog_panel enable {dialog_panel find opt1.btn}})
   (SELECT_MSG
      {ui pop_screen}
      {net_sync enable}
   )
}
{new BandScreen
   story_hub_screen
   (panels meta story_music background_panel story_hub_panel)
   (focus story_hub_panel)
   (helpbar
      {if_else {is_leader_local}
         (
            (cancel helpbar_back)
            (confirm helpbar_select)
         )
         ()
      }
   )
   (back {gamemode get matchmaking_screen})
   (background_view story_hub)
}
{new StoryPrizePanel
   story_prizes_panel
   (file "story_prizes.milo")
   (focus "prizes.lst")
   (enter {story_music start_ambient_sound})
   (exit {story_music stop_ambient_sound})
   (SELECT_MSG
      {if {$this is_up}
         {if {$this selection_is_playable_video}
            {$this handle_select_video_watched}
            {$this set_saved_prize_position}
            {story_mediaplay_panel set videos {$this get_selected_video}}
            {ui goto_screen story_mediaplay_screen}
         }
      }
   )
   (SCROLL_MSG
      {if {$this is_up}
         {$this refresh_helpbar}
      }
   )
   (TRANSITION_COMPLETE_MSG {$this refresh_helpbar})
   (refresh_helpbar
      {if_else {$this selection_is_playable_video}
         {helpbar set_widget confirm prize_video_play}
         {helpbar set_widget confirm ''}
      }
   )
   (BUTTON_DOWN_MSG
      {if_else
         {&&
            $user
            {== {$user connected_controller_type} kControllerVocals}
            {== $raw_button kPad_L1}
         }
         {do
            {$this goto_previous_category}
            {play_instr_sfx $user button_toggle}
            kDataUnhandled
         }
         {switch $action
            (kAction_Confirm
               {if_else {! {$this selection_is_playable_video}}
                  0
                  kDataUnhandled
               }
            )
            (kAction_Option
               {$this goto_next_category}
               {play_instr_sfx $user button_toggle}
               kDataUnhandled
            )
            #ifdef HX_WII
            (kAction_Option1
               {$this goto_next_category}
               {play_instr_sfx $user button_toggle}
               kDataUnhandled
            )
            #endif
            kDataUnhandled
         }
      }
   )
}
{new BandScreen
   story_prizes_screen
   (panels meta story_music story_prizes_panel)
   (focus story_prizes_panel)
   (back story_hub_screen)
   (helpbar
      (
         (cancel helpbar_back)
         (option helpbar_next_heading)
      )
   )
}
{new StoryMemorabiliaPanel
   story_memorabilia_panel
   (file "story_memorabilia.milo")
   (focus "memorabilia.lst")
   (helpbar
      (
         (cancel helpbar_back)
         (option helpbar_next_chapter)
      )
   )
   (enter
      {platform_mgr enable_xmp}
      {story_music start_ambient_sound}
   )
   (exit {story_music stop_ambient_sound})
   (SCROLL_MSG {$this refresh_helpbar})
   (TRANSITION_COMPLETE_MSG {$this refresh_helpbar})
   (refresh_helpbar
      {if {$this is_up}
         {if_else {$this is_showing_closeup}
            {if_else {$this is_showing_zoomed_info}
               {helpbar set_config
                  (
                     (cancel memo_hide_closeup)
                     (option memo_hide_closeup_text)
                  )
               }
               {helpbar set_config
                  (
                     (cancel memo_hide_closeup)
                     (option memo_show_closeup_text)
                  )
               }
            }
            {do
               {helpbar set_config
                  (
                     (cancel helpbar_back)
                     (option helpbar_next_chapter)
                  )
               }
               {if {$this selection_is_unlocked}
                  {helpbar set_widget confirm memo_view_closeup}
               }
            }
         }
      }
   )
   (handle_next_category
      ($user)
      {play_instr_sfx $user button_toggle}
   )
   (handle_previous_category
      ($user)
      {play_instr_sfx $user button_toggle}
   )
   (handle_toggle_text
      ($user)
      {play_instr_sfx $user button_toggle}
   )
   (handle_hide_closeup
      ($user)
      {play_instr_sfx $user button_back}
   )
   (handle_scroll_photo
      ($user)
      {play_instr_sfx $user button_toggle}
   )
}
{new BandScreen
   story_memorabilia_screen
   (panels meta story_music story_memorabilia_panel)
   (focus story_memorabilia_panel)
   (helpbar
      (
         (cancel helpbar_back)
         (option helpbar_next_chapter)
      )
   )
   (back story_hub_screen)
   (background_view memorabilia)
}
{new BandScreen
   story_songselect_photos_screen
   (panels meta story_music story_memorabilia_panel)
   (focus story_memorabilia_panel)
   (back story_chapter_songselect_screen)
   (background_view memorabilia)
   (song_symbol '')
   (enter
      {story_memorabilia_panel select_associated_photo_for_song [song_symbol]}
   )
   (exit {platform_mgr disable_xmp})
}
{new BandScreen
   story_challengeselect_photos_screen
   (panels meta story_music story_memorabilia_panel)
   (focus story_memorabilia_panel)
   (back story_chapter_challenges_screen)
   (background_view memorabilia)
   (song_symbol '')
   (enter
      {story_memorabilia_panel select_associated_photo_for_challenge
         [song_symbol]
      }
   )
   (exit {platform_mgr disable_xmp})
}
{new StoryMediaPanel
   story_media_panel
   (file "story_media.milo")
   (focus "media.lst")
   (SELECT_MSG
      {story_mediaplay_panel set videos {$this get_selected_media}}
      {ui push_screen story_mediaplay_screen}
   )
   (TRANSITION_COMPLETE_MSG {$this refresh_helpbar})
   (refresh_helpbar
      {helpbar set_config
         (
            (confirm prize_video_play)
            (cancel helpbar_back)
         )
      }
   )
}
{new BandScreen
   story_media_screen
   (panels meta story_music story_media_panel)
   (focus story_media_panel)
   (back story_hub_screen)
   (helpbar
      (
         (confirm prize_video_play)
         (cancel helpbar_back)
      )
   )
}
{new UIPanel
   story_hint_panel
   (file "story_hint.milo")
   (force_exit TRUE)
   (hint "")
   (enter
      {$this set hint {story get_first_hint}}
      {name.lbl set_token_fmt
         {story get_hint_name_token {$this get hint}}
         {story get_story_owner_name}
      }
      {description.lbl set
         text_token
         {story get_hint_description_token {$this get hint}}
      }
      #ifndef HX_WII
      {story apply_story_owner_name gamertag.lbl}
      #endif
      {story mark_hint_seen {$this get hint}}
   )
}
{new BandScreen
   story_hint_screen
   (panels meta story_music story_hint_panel)
   (focus story_hint_panel)
   (helpbar
      {if_else {is_leader_local}
         ((confirm helpbar_continue))
         ()
      }
   )
   (destination_screen null)
   (BUTTON_DOWN_MSG
      {if {== $action kAction_Confirm}
         {play_instr_sfx $user button_select}
         {ui goto_screen {get_story_destination_screen [destination_screen]} TRUE}
      }
      kDataUnhandled
   )
}
{new StoryChapterListPanel
   story_chapter_panel
   (file "story_chapterlist.milo")
   (register_for_content TRUE)
   (enter {{story progress} clear_last_set_state})
   (SELECT_MSG {ui goto_screen story_chapter_songselect_screen})
   (scroll_chapter
      ($user)
      {play_instr_sfx $user button_toggle}
   )
}
{new BandScreen
   story_chapter_screen
   (panels meta story_music story_chapter_panel)
   (helpbar
      {if_else {is_leader_local}
         (
            (cancel helpbar_back)
            (confirm helpbar_select_chapter)
         )
         ()
      }
   )
   (focus story_chapter_panel)
   (back story_hub_screen)
   (enter
      {input_mgr set_limit kLimitSessionLeader}
      {story_music music_start}
      {if_else {is_leader_local}
         {helpbar set_config
            (
               (cancel helpbar_back)
               (confirm helpbar_select_chapter)
            )
         }
         {helpbar set_config ()}
      }
   )
   (TRANSITION_COMPLETE_MSG
      {story_chapter_panel show_info}
      {platform_mgr enable_xmp}
   )
}
{new StoryChapterSongSelectPanel
   story_chapter_songselect_panel
   (file "story_chapter_songselect.milo")
   (focus "songs.lst")
   (enter
      {{story progress} clear_last_set_state}
      {name.lbl set text_token {{story chapter} chapter_name}}
   )
   (BUTTON_DOWN_MSG
      {switch $action
         (kAction_Confirm
            {do
               {play_instr_sfx $user button_select}
               kDataUnhandled
            }
         )
         (kAction_Cancel {$this clear_song} kDataUnhandled)
         (kAction_Option
            {if {$this can_view_photos}
               {story_songselect_photos_screen set song_symbol {$this selected_song}}
               {play_instr_sfx $user button_shortcut}
               {$this set_current_song}
               {ui goto_screen story_songselect_photos_screen}
            }
         )
         #ifdef HX_WII
         (kAction_Option1
            {if {$this can_view_photos}
               {story_songselect_photos_screen set song_symbol {$this selected_song}}
               {play_instr_sfx $user button_shortcut}
               {$this set_current_song}
               {ui goto_screen story_songselect_photos_screen}
            }
         )
         #endif
         kDataUnhandled
      }
   )
   (SCROLL_MSG
      {if {$this is_up}
         {$this refresh_helpbar}
      }
   )
   (TRANSITION_COMPLETE_MSG {$this refresh_helpbar})
   (refresh_helpbar
      {if_else {$this can_view_photos}
         {helpbar set_widget option view_photos}
         {helpbar set_widget option ''}
      }
   )
   (song_selected
      {{story performer} select_venue}
      {ui goto_screen story_chapter_seldiff_screen}
   )
}
{new BandScreen
   story_chapter_songselect_screen
   (panels
      meta
      story_music
      story_chapter_panel
      story_chapter_songselect_panel
      postsong_sfx_panel
   )
   (focus story_chapter_songselect_panel)
   (back story_chapter_screen)
   (enter
      {platform_mgr disable_xmp}
      {story_music music_stop}
      {story_chapter_panel hide_info}
      {input_mgr set_limit kLimitSessionLeader}
      {helpbar set_config ((cancel helpbar_back))}
      {if {is_leader_local}
         {helpbar set_widget confirm story_song_play}
      }
   )
}
{new StoryChapterChallengeSelectPanel
   story_chapter_challenges_panel
   (file "story_chapter_challenges.milo")
   (focus "challenges.lst")
   (enter
      {platform_mgr disable_xmp}
      {{story progress} clear_last_set_state}
   )
   (BUTTON_DOWN_MSG
      {switch $action
         (kAction_Cancel {$this clear_challenge} kDataUnhandled)
         (kAction_Option
            {if {$this can_view_photos}
               {story_challengeselect_photos_screen set
                  song_symbol
                  {$this selected_challenge}
               }
               {play_instr_sfx $user button_shortcut}
               {$this set_current_challenge}
               {ui goto_screen story_challengeselect_photos_screen}
            }
         )
         #ifdef HX_WII
         (kAction_Option1
            {if {$this can_view_photos}
               {story_challengeselect_photos_screen set
                  song_symbol
                  {$this selected_challenge}
               }
               {play_instr_sfx $user button_shortcut}
               {$this set_current_challenge}
               {ui goto_screen story_challengeselect_photos_screen}
            }
         )
         #endif
         kDataUnhandled
      }
   )
   (SCROLL_MSG
      {if {$this is_up}
         {$this refresh_helpbar}
      }
   )
   (TRANSITION_COMPLETE_MSG {$this refresh_helpbar})
   (refresh_helpbar
      {if_else {$this can_view_photos}
         {helpbar set_widget option view_photos}
         {helpbar set_widget option ''}
      }
   )
   (challenge_selected
      {{story performer} select_venue}
      {ui goto_screen story_chapterchallenge_seldiff_screen}
   )
}
{new BandScreen
   story_chapter_challenges_screen
   (panels meta story_chapter_challenges_panel postsong_sfx_panel)
   (focus story_chapter_challenges_panel)
   (back story_hub_screen)
   (enter
      {input_mgr set_limit kLimitSessionLeader}
      {if_else {is_leader_local}
         {helpbar set_config
            (
               (cancel helpbar_back)
               (confirm story_chapterchallenge_play)
            )
         }
         {helpbar set_config ()}
      }
   )
   (exit {$this stop_preview})
}
{new StoryChapterSetCompletePanel
   story_chapter_setcomplete_panel
   (file "story_chapter_setcomplete.milo")
   (focus "")
   (BUTTON_DOWN_MSG
      {if {== $action kAction_Confirm}
         {if_else {$this is_ready_to_continue}
            {do
               {play_instr_sfx $user button_select}
               {if_else {story story_haspostsong_screen}
                  {ui goto_screen {story story_postsong_screen}}
                  {ui pop_screen meta_loading_continue_screen}
               }
            }
            {$this enable_fast_forward}
         }
      }
      kDataUnhandled
   )
   (enter
      {do
         ($performer {story performer})
         ($chapter {story chapter})
         {stars.sd set_values
            {$performer chapter_songset_stars}
            {$performer chapter_songset_total_stars}
         }
         {score.lbl set_int {$performer chapter_songset_score} TRUE}
         {$performer apply_story_set_complete_title heading.lbl}
      }
      {$this refresh_helpbar}
   )
   (refresh_helpbar
      {if_else {&& {$this is_ready_to_continue} {is_leader_local}}
         {helpbar set_config ((confirm helpbar_continue))}
         {helpbar set_config ()}
      }
   )
}
{new BandScreen
   story_chapter_setcomplete_screen
   (panels story_chapter_setcomplete_panel)
   (focus story_chapter_setcomplete_panel)
   (scroll_sfx FALSE)
   (update_helpbar {story_chapter_setcomplete_panel refresh_helpbar})
}
{new MoviePanel
   story_chaptermovie_panel
   MOVIE_PANEL_STUFF
   (videos {story get_chaptervideo})
   (movie_done
      {if {is_leader_local}
         {story handle_current_chaptervideo_seen}
         {{story performer} select_most_recent_chapter}
         {ui goto_screen
            {get_story_destination_screen
               {story_chaptermovie_screen get destination_screen}
            }
         }
      }
   )
}
{new BandScreen
   story_chaptermovie_screen
   (panels meta story_chaptermovie_panel)
   (send_net_sfx FALSE)
   (focus story_chaptermovie_panel)
   (destination_screen null)
   (helpbar ())
   (enter {platform_mgr disable_xmp})
}
{new MoviePanel
   story_endmovie_panel
   MOVIE_PANEL_STUFF
   (videos outro)
   (movie_done
      {if {is_leader_local}
         {ui goto_screen credits_screen}
      }
   )
}
{new BandScreen
   story_endmovie_screen
   (panels story_endmovie_panel)
   (send_net_sfx FALSE)
   (focus story_endmovie_panel)
   (helpbar ())
   (enter
      {coop_track_panel set_showing FALSE}
      {achievements set_allow_achievements FALSE}
      {passive_messages_panel set_allow_messages FALSE}
      {platform_mgr disable_xmp}
      #ifdef HX_WII
      {rnd frames_buffered 1}
      #endif
   )
   (exit
      {coop_track_panel set_showing TRUE}
      {achievements set_allow_achievements TRUE}
      {passive_messages_panel set_allow_messages TRUE}
      #ifdef HX_WII
      {rnd frames_buffered 2}
      #endif
   )
}
{new MoviePanel
   story_mediaplay_panel
   (skip_text story_movie_return_to_theatre)
   MOVIE_PANEL_STUFF
   (videos "")
   (movie_done {ui goto_screen story_prizes_screen})
}
{new BandScreen
   story_mediaplay_screen
   (panels meta story_mediaplay_panel)
   (focus story_mediaplay_panel)
   (helpbar ())
   (enter {platform_mgr disable_xmp})
   (exit {platform_mgr enable_xmp})
}