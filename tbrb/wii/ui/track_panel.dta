#define TRACK_END_FRAME (1920)
#define TRACK_PANEL_HANDLERS (
   (enter
      {game add_sink $this}
      {$this set_showing
         {'||'
            $skip_intro
            {gamemode in_mode drum_trainer}
            {! {gamemode in_mode practice}}
         }
      }
      {do
         ($vocals 0)
         ($gems 0)
         ($tri_frame 0)
         {beatmatch foreach_active_player $player
            {if_else {== {$player instrument} vocals}
               {'++' $vocals}
               {'++' $gems}
            }
         }
         {if {'||' {&& $vocals {== $gems 1}} {&& {== $vocals 0} {>= $gems 2}}}
            {set $tri_frame {song_mgr tri_frame {meta_performer song}}}
         }
         {if {&& $vocals {>= $gems 2}}
            {set $tri_frame 1}
         }
         {rnd tri_frame $tri_frame}
      }
      #ifdef HX_WII
      {rnd frames_buffered 2}
      #endif
   )
   (exit
      #ifdef HX_WII
      {rnd frames_buffered 1}
      #endif
      {game remove_sink $this}
   )
   (unison_hit)
   (unison_miss)
   (set_track_out)
   (set_track_in)
   (animate_track ($start $end $period $units))
   (animate_track_out)
   (animate_track_out_fast)
   (animate_track_in_fast)
   (finish_load
      {$this set_showing FALSE}
      {$this set_track_out}
   )
   (intro_start
      {$this track_reset}
      {$this set_showing TRUE}
      {$this set_track_in}
   )
   (intro_skip
      {$this track_reset}
      {$this set_showing TRUE}
      {$this set_track_out}
      {if {&& {exists gamemode} {! {gamemode get is_tutorial}}}
         {$this play_intro}
      }
   )
   (on_reset
      {beatmatch foreach_active_player $m
         {$m on_new_track}
         {{$m track} init {$m track}}
      }
      {{$this loaded_dir} reset}
   )
   (on_extend
      {$this play_intro}
      {if {&& {exists gamemode} {gamemode get play_track_intro_sfx}}
         {synth play track_beg.cue}
      }
   )
)
{new TrackPanel
   coop_track_panel
   (file "track/trackpanel.milo")
   TRACK_PANEL_HANDLERS
}
{new TrackPanel
   h2h_track_panel
   (file "track/trackpanel_h2h.milo")
   TRACK_PANEL_HANDLERS
}
{func get_track_panel
   {gamemode get track_panel}
}
{set $playback_file ""}