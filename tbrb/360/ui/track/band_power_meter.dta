(RndDir
   (types
      (powermeter
         (editor
            (initialize
               script
               (script
                  {$this reset_milo}
                  {$this set_showing TRUE}
               )
            )
            (test_energy
               float
               (post_sync {$this set_energy [test_energy] [state] guitar})
            )
            (energy_added script (script {$this energy_added}))
            (energy_ready script (script {$this energy_ready guitar TRUE}))
            (stop script (script {$this stop_deploy}))
            (disable_overdrive script (script {$this set_no_overdrive}))
            (vocal_meter bool (read_only TRUE))
         )
         (test_energy 0)
         (energy 0)
         (state uninitialized)
         (vocal_meter FALSE)
         (set_energy
            ($in_val $state $instrument)
            {set [energy] {extend_anim.grp frame}}
            {if {!= $in_val [energy]}
               {extend_anim.grp animate (range $in_val $in_val)}
               {set [energy] $in_val}
            }
            {if {!= $state [state]}
               {switch $state
                  (none {$this stop_deploy})
                  (ready {$this energy_ready $instrument TRUE})
                  (deploying {$this deploy})
               }
               {set [state] $state}
            }
         )
         (force_set_energy
            ($in_val $state $instrument)
            {extend_anim.grp animate (range $in_val $in_val)}
            {set [energy] $in_val}
            {switch $state
               (none {$this stop_deploy})
               (ready {$this energy_ready $instrument FALSE})
               (deploying {$this deploy})
            }
            {set [state] $state}
         )
         (reset_milo
            {pulse_milo.trig trigger}
            {$this reset}
         )
         (reset
            {reset.trig trigger}
            {$this set_energy 0 none guitar}
         )
         (energy_ready
            ($instrument $play_sfx)
            {do
               ($cue {sprintf "rp_available_%s.cue" $instrument})
               {be_ready.trig trigger}
               {if {== $play_sfx TRUE}
                  {synth play $cue}
               }
            }
         )
         (deploy {be_deploying.trig trigger})
         (stop_deploy
            {be_filling.trig trigger}
            {extend_anim.grp set_frame [energy]}
         )
         (energy_added {spotlight_phrase_success.trig trigger})
         (set_no_overdrive {no_overdrive.trig trigger})
      )
   )
)