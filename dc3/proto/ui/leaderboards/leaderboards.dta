{new HamPanel
   leaderboards_panel
   (file "leaderboards.milo")
   (focus right_hand.hnl)
   (tertiary_token leaderboards_view_self)
   (back_token back)
   (use_gamertag_bg TRUE)
   (loading FALSE)
   (enter
      {start_loading.flow activate}
      {set [tertiary_token] leaderboards_view_self}
      {leaderboards set_mode 1}
      {if_else {$this connected_to_live}
         {do
            {set [loading] TRUE}
            {leaderboards download_scores {gamedata get song}}
         }
         {$this leaderboards_failed_live}
      }
      {$this hide_list}
      {$this update_title}
   )
   (hide_list
      {right_hand.hnl set_provider empty.hnp}
      {right_hand.hnl disable_navigation}
   )
   (show_list
      {right_hand.hnl set_provider leaderboards}
      {right_hand.hnl enable_navigation}
   )
   (exit)
   (update_title
      {switch {leaderboards get_type}
         (0 {screentitle.lbl set text_token leaderboards_title_solo})
         (1 {screentitle.lbl set text_token leaderboards_title_coop})
         (3 {screentitle.lbl set text_token leaderboards_title_career})
         (4 {screentitle.lbl set text_token leaderboards_title_inf_score})
         (5 {screentitle.lbl set text_token leaderboards_title_inf_endurance})
      }
   )
   (leaderboards_ethernet_disconnected
      {ui goto_screen leaderboards_ethernet_disconnected_screen}
   )
   (leaderboards_loaded
      {set [loading] FALSE}
      {finish_loading.flow activate}
      {$this show_list}
      {right_hand.hnl refresh}
      {$this set_up {leaderboards num_scores}}
   )
   (connected_to_live
      {if_else {profile_mgr has_active_profile}
         {do
            ($profile {profile_mgr get_active_profile})
            {if_else {platform_mgr is_pad_signed_into_live {$profile get_pad_num}}
               1
               0
            }
         }
         0
      }
   )
   (leaderboards_failed_live
      {set [loading] FALSE}
      {finish_loading.flow activate}
      {must_sign_in.lbl set_showing TRUE}
      {rc_unavailable.lbl set_showing FALSE}
   )
   (leaderboards_failed_rc
      {set [loading] FALSE}
      {finish_loading.flow activate}
      {rc_unavailable.lbl set_showing TRUE}
      {must_sign_in.lbl set_showing FALSE}
   )
   (set_focus
      ($focus_index)
      {if {!= $focus_index -1}
         {right_hand.hnl scroll_to_index 0 0}
         {right_hand.hnl scroll_to_index $focus_index -1}
      }
   )
   (set_up
      ($num_scores)
      {must_sign_in.lbl set_showing FALSE}
      {rc_unavailable.lbl set_showing FALSE}
      {no_any_scores.lbl set_showing FALSE}
      {no_friend_career_scores.lbl set_showing FALSE}
      {no_friend_scores.lbl set_showing FALSE}
      {no_self_score.lbl set_showing FALSE}
      {if_else {== $num_scores 0}
         {do
            {switch {leaderboards get_mode}
               (0 {no_any_scores.lbl set_showing TRUE})
               (2
                  {if_else {== {leaderboards get_type} 3}
                     {no_friend_career_scores.lbl set_showing TRUE}
                     {no_friend_scores.lbl set_showing TRUE}
                  }
               )
               (1 {no_self_score.lbl set_showing TRUE})
            }
         }
         {if {&& {! {leaderboards has_self}} {== {leaderboards get_mode} 1}}
            {no_self_score.lbl set_showing TRUE}
         }
      }
   )
   (NAV_SELECT_MSG
      {switch $component
         (right_hand.hnl
            {if_else {leaderboards is_self $index}
               {do
                  {run_flow invalid_select.flow}
                  skip_select_anim_and_sound
               }
               {do
                  {leaderboards show_gamercard $index {profile_mgr get_active_profile}}
                  skip_select_anim
               }
            }
         )
         (left_hand.hnl
            {switch $name
               (back {ui go_back_screen {{ui current_screen} get back}})
               (leaderboards_view_top
                  {if {== [loading] FALSE}
                     {if_else {$this connected_to_live}
                        {do
                           {set [tertiary_token] leaderboards_view_self}
                           {helpbar resync}
                           {leaderboards set_mode 1}
                           {$this update_sort}
                        }
                        {$this leaderboards_failed_live}
                     }
                  }
               )
               (leaderboards_view_friends
                  {if {== [loading] FALSE}
                     {if_else {$this connected_to_live}
                        {do
                           {set [tertiary_token] leaderboards_view_top}
                           {helpbar resync}
                           {leaderboards set_mode 0}
                           {$this update_sort}
                        }
                        {$this leaderboards_failed_live}
                     }
                  }
               )
               (leaderboards_view_self
                  {if {== [loading] FALSE}
                     {if_else {$this connected_to_live}
                        {do
                           {set [tertiary_token] leaderboards_view_friends}
                           {helpbar resync}
                           {leaderboards set_mode 2}
                           {$this update_sort}
                        }
                        {$this leaderboards_failed_live}
                     }
                  }
               )
            }
         )
      }
   )
   (update_sort
      {set [loading] TRUE}
      {$this hide_list}
      {start_loading.flow activate}
      {leaderboards download_scores {gamedata get song}}
   )
}
{new HamScreen
   leaderboards_screen
   (panels meta background_panel leaderboards_panel)
   (focus leaderboards_panel)
   (back
      {if_else {'||' {== {leaderboards get_type} 4} {== {leaderboards get_type} 5}}
         {symbol infinite_playlist_screen}
         {symbol leaderboard_select_screen}
      }
   )
   (on_voice_command
      ($command)
      {switch $command
         BACRO
      }
   )
}
{new HamScreen
   leaderboards_ethernet_disconnected_screen
   (panels meta background_confirmation_panel dialog_panel)
   (focus dialog_panel)
   (error error_ethernet_unplugged)
   (enter {dialog_panel set_ok [error] FALSE})
   (NAV_SELECT_MSG {ui goto_screen main_screen})
}