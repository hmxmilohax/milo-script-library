{new HamPanel
   campaign_choose_mode_panel
   (file "campaign_choose_mode.milo")
   (focus right_hand.hnl)
   (back_token back)
   (enter
      {hamprovider set ui_nav_mode shell}
      {choose_mode_provider update_list FALSE}
      {right_hand.hnl set_provider choose_mode_provider}
      {song_info_panel set_showing TRUE}
      {song_info_panel set_song_list_mode FALSE}
      {voice_input_panel activate_voice_context campaign_choose_mode_screen}
   )
   SELECT_RIGHT_LIST_TOKEN
   (poll SLIDE_RIGHT_LIST_ITEM)
   (NAV_SELECT_MSG
      {switch $name
         (perform {$this handle_select_mode_perform})
         (practice {$this handle_select_mode_practice})
         (back {$this handle_select_mode_cancel})
      }
   )
   (NAV_HIGHLIGHT_MSG)
   (BUTTON_DOWN_MSG
      {switch $action
         (
            (kAction_Confirm)
            {set $mode_sel {right_hand.hnl get_selected}}
            {if_else {== $mode_sel 0}
               {$this handle_select_mode_perform}
               {$this handle_select_mode_practice}
            }
         )
         (
            (kAction_Cancel)
            {$this handle_select_mode_cancel}
         )
         kDataUnhandled
      }
   )
   (start_game
      {hamprovider set crew_character {meta_performer get_challenge_character}}
      {hamprovider set crew_complete {meta_performer is_current_era_complete}}
      {enter_gameplay}
   )
   (handle_select_mode_perform
      {gamemode set_mode campaign_perform}
      {update_campaign_work_it_flag}
      {set $prev_state {campaign get_campaign_state}}
      {set $next_state {step_campaign_state_forward}}
      {if_else {== $next_state kCampaignStatePerformIt}
         {$this start_game}
         {campaign_flow_failure campaign_choose_mode_panel $prev_state $next_state}
      }
   )
   (handle_select_mode_practice
      {gamemode set_mode campaign_practice}
      {set $prev_state {campaign get_campaign_state}}
      {set $next_state {step_campaign_state_forward}}
      {if_else {== $next_state kCampaignStatePracticeSetup}
         {ui goto_screen startgame_campaign_practice_screen}
         {campaign_flow_failure campaign_choose_mode_panel $prev_state $next_state}
      }
   )
   (handle_select_mode_cancel
      {unless {ui in_transition}
         {set $back_screen {step_campaign_state_backward}}
         {ui go_back_screen $back_screen}
      }
   )
}
{new HamScreen
   campaign_choose_mode_screen
   (panels meta background_panel campaign_choose_mode_panel song_info_panel)
   (focus campaign_choose_mode_panel)
   (back campaign_songselect_screen)
   (enter {campaign set_campaign_state kCampaignStateModeSelect})
   (on_voice_command
      ($command)
      {switch $command
         BACRO
         {campaign_choose_mode_panel select_right_list_token $command}
      }
   )
}