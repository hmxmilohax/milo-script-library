{new FitnessProvider fitness_provider}
{new HamPanel
   fitness_panel
   (file "fitness.milo")
   (focus right_hand.hnl)
   (focus_index 0)
   (top_index 0)
   (back_token screen_name_save_return)
   (helpbar_confirm_label help_select)
   (use_gamertag_bg TRUE)
   (curr_view fitness)
   (allow_voice_control TRUE)
   (fitness_goal_idx 0)
   (fitness_goal_set_days_idx 0)
   (fitness_goal_set_days_top_idx 0)
   (fitness_goal_set_calorie_idx 0)
   (fitness_goal_set_calorie_top_idx 0)
   (enter
      {set [fitness_goal_idx] 0}
      {set [fitness_goal_set_days_idx] 0}
      {set [fitness_goal_set_days_top_idx] 0}
      {set [fitness_goal_set_calorie_idx] 0}
      {set [fitness_goal_set_calorie_top_idx] 0}
      {memcardmgr add_sink $this (save_load_all_complete_msg)}
      {hamprovider add_sink $this (on_ui_nav_player_change) 0 FALSE}
      {player_provider_1 add_sink $this (padnum_updated)}
      {player_provider_2 add_sink $this (padnum_updated)}
      {set [curr_view] fitness}
      {fitness_provider update_list}
      {$this update_profile}
      {right_hand.hnl scroll_to_index [focus_index] [top_index]}
      {$this set allow_voice_control TRUE}
      {voice_input_panel activate_voice_context fitness_screen}
      {fitness_goal_mgr smart_glass_listen 1}
      {calorie_provider sort_with_headers TRUE}
      {$this update_all}
   )
   (exit
      {memcardmgr remove_sink $this}
      {hamprovider remove_sink $this on_ui_nav_player_change}
      {player_provider_1 remove_sink $this padnum_updated}
      {player_provider_2 remove_sink $this padnum_updated}
      {set [top_index] {right_hand.hnl get_top_index}}
      {fitness_goal_mgr smart_glass_listen 0}
   )
   (unload {calorie_provider unload})
   HIGHLIGHT_RIGHT_LIST_ITEM
   SELECT_RIGHT_LIST_TOKEN
   (poll SLIDE_RIGHT_LIST_ITEM)
   (NAV_SELECT_MSG
      {switch $component
         (right_hand.hnl
            {switch $name
               (fitness_option
                  {do
                     ($profile {profile_mgr get_active_profile})
                     {$profile toggle_fitness_mode}
                     {$this update_all}
                     skip_select_anim
                  }
               )
               (fitness_body_profile
                  {do
                     ($profile {profile_mgr get_active_profile})
                     {platform_mgr show_fitness_body_profile {$profile get_pad_num}}
                     skip_select_anim
                  }
               )
               (fitness_set_fitness_goals {ui push_screen goal_reset_warning_screen})
               (playlists
                  {song_select_playlist_screen set back_screen fitness_screen}
                  {ui goto_screen song_select_playlist_tutorial_check_screen}
               )
               {if {== [curr_view] fitness_goal_set_calorie}
                  {calorie_provider on_select $index}
               }
            }
         )
         (left_hand.hnl
            {switch $name
               (screen_name_save_return
                  {if {== [curr_view] fitness}
                     {$this go_back}
                  }
               )
            }
         )
      }
   )
   (NAV_SELECT_DONE_MSG
      {switch $component
         (right_hand.hnl
            {switch $name
               (fitness_goal_set_days_active_goal
                  {$this change_view fitness_goal_set_days_active FALSE}
               )
               (fitness_goal_set_calorie_goal
                  {$this change_view fitness_goal_set_calorie FALSE}
               )
               {switch [curr_view]
                  (fitness_goal_set_days_active
                     {do
                        ($profile {profile_mgr get_active_profile})
                        {$profile send_fitness_goal_target_days {'+' $index 1}}
                        {$this change_view fitness_goal FALSE}
                        {fitness_info_panel update_all}
                     }
                  )
                  (fitness_goal_set_calorie
                     {if_else {calorie_provider is_data_header $index}
                        {do
                           ($header_name {calorie_provider get_token $index})
                           ($new_index)
                           {calorie_provider on_select_done $index}
                           {right_hand.hnl refresh}
                           {set $new_index {calorie_provider first_data_index $header_name}}
                           {right_hand.hnl scroll_to_index $new_index $new_index}
                           {right_hand.hnl play_enter_anim}
                        }
                        {do
                           {$this change_view fitness_goal FALSE}
                           {fitness_info_panel update_all}
                        }
                     }
                  )
               }
            }
         )
         (left_hand.hnl
            {switch $name
               (screen_name_save_return
                  {if {!= [curr_view] fitness}
                     {$this go_back}
                  }
               )
            }
         )
      }
   )
   (NAV_HIGHLIGHT_MSG
      {switch $component
         (right_hand.hnl
            {switch [curr_view]
               (fitness_goal {set [fitness_goal_idx] $index})
               (fitness_goal_set_days_active {set [fitness_goal_set_days_idx] $index})
               (fitness_goal_set_calorie
                  {calorie_provider set_highlighted_ix $index}
                  {set [fitness_goal_set_calorie_idx] $index}
               )
               (fitness {set [focus_index] $index})
            }
         )
      }
   )
   (BUTTON_DOWN_MSG
      {if_else {== $action kAction_Cancel}
         {$this go_back}
         kDataUnhandled
      }
   )
   (update_all
      {fitness_info_panel update_all}
      {right_hand.hnl enable_navigation}
      {if_else {profile_mgr has_active_profile}
         {do
            {switch [curr_view]
               (fitness_goal_set_days_active
                  {set [fitness_goal_set_days_top_idx] {right_hand.hnl get_top_index}}
               )
               (fitness_goal_set_calorie
                  {set [fitness_goal_set_calorie_top_idx] {right_hand.hnl get_top_index}}
                  {calorie_provider exit}
               )
            }
            {set [curr_view] fitness}
            {right_hand.hnl set list_dir_resource label_checkbox}
            {voice_input_panel activate_voice_context fitness_screen}
            {has_profile.flow activate}
            {right_hand.hnl set_provider fitness_provider}
            {right_hand.hnl refresh}
            {$this set helpbar_confirm_label help_select}
            {helpbar resync}
            {fitness_disclaimer.lbl set_showing
               {! {{profile_mgr get_active_profile} in_fitness_mode}}
            }
         }
         {do
            {no_profile.flow activate}
            {right_hand.hnl set nav_provider provider_empty.hnp}
            {right_hand.hnl refresh}
            {$this set helpbar_confirm_label none}
            {helpbar resync}
            {fitness_disclaimer.lbl set_showing FALSE}
            {if {== {ui current_screen} goal_reset_warning_screen}
               {ui pop_screen}
            }
         }
      }
   )
   (save_load_all_complete_msg {$this check_profile})
   (padnum_updated {$this check_profile})
   (on_ui_nav_player_change {$this check_profile})
   (is_loaded {content_mgr refresh_done})
   (load {content_loading_panel allowed_to_show TRUE})
   (finish_load {content_loading_panel allowed_to_show FALSE})
   (calorie_selected
      ($calorie)
      {do
         ($profile {profile_mgr get_active_profile})
         {$profile send_fitness_goal_target_calories $calorie}
      }
   )
   (sync_failed {$this update_all})
   (fitness_goal_synced {$this update_all})
   (sync_fitness_goal_with_rc
      {loading.flow activate}
      {right_hand.hnl set nav_provider provider_empty.hnp}
      {right_hand.hnl disable_navigation}
      {right_hand.hnl refresh}
      {$this set helpbar_confirm_label none}
      {helpbar resync}
      {fitness_goal_mgr get_fitness_goal_from_rc}
   )
   (update_profile
      {if_else {fitness_goal_mgr has_valid_profile}
         {$this sync_fitness_goal_with_rc}
         {$this update_all}
      }
   )
   (check_profile
      {if_else {fitness_goal_mgr is_profile_changed}
         {$this update_profile}
         {$this update_all}
      }
   )
   (change_view
      ($new_view $reset)
      {switch [curr_view]
         (fitness_goal_set_days_active
            {set [fitness_goal_set_days_top_idx] {right_hand.hnl get_top_index}}
         )
         (fitness_goal_set_calorie
            {set [fitness_goal_set_calorie_top_idx] {right_hand.hnl get_top_index}}
            {calorie_provider exit}
         )
      }
      {set [curr_view] $new_view}
      {switch $new_view
         (fitness_goal
            {if {== $reset TRUE}
               {do
                  ($profile {profile_mgr get_active_profile})
                  {profile_mgr set_critical_profile $profile}
                  {$profile reset_fitness_goal}
                  {fitness_info_panel update_all}
                  {set [fitness_goal_idx] 0}
                  {set [fitness_goal_set_days_idx] 0}
                  {set [fitness_goal_set_days_top_idx] 0}
                  {set [fitness_goal_set_calorie_idx] 0}
                  {set [fitness_goal_set_calorie_top_idx] 0}
               }
            }
            {right_hand.hnl set list_dir_resource label_checkbox}
            {right_hand.hnl set nav_provider new_goal.hnp}
            {right_hand.hnl refresh}
            {right_hand.hnl scroll_to_index [fitness_goal_idx] [fitness_goal_idx]}
            {voice_input_panel activate_voice_context ''}
         )
         (fitness
            {do
               ($profile {profile_mgr get_active_profile})
               {$profile send_fitness_goal_to_rc}
               {profile_mgr clear_critical_profile}
               {saveload_mgr autosave}
               {right_hand.hnl set list_dir_resource label_checkbox}
               {$this update_profile}
               {right_hand.hnl scroll_to_index [focus_index] [top_index]}
            }
            {voice_input_panel activate_voice_context fitness_screen}
         )
         (fitness_goal_set_days_active
            {right_hand.hnl set list_dir_resource label_checkbox}
            {right_hand.hnl set nav_provider days_goal.hnp}
            {right_hand.hnl refresh}
            {right_hand.hnl scroll_to_index
               [fitness_goal_set_days_idx]
               [fitness_goal_set_days_top_idx]
            }
         )
         (fitness_goal_set_calorie
            {calorie_provider enter}
            {right_hand.hnl set list_dir_resource list_calorie}
            {right_hand.hnl set_provider calorie_provider}
            {right_hand.hnl refresh}
            {right_hand.hnl scroll_to_index
               [fitness_goal_set_calorie_idx]
               [fitness_goal_set_calorie_top_idx]
            }
         )
      }
      {right_hand.hnl play_enter_anim}
   )
   (go_back
      {switch [curr_view]
         (fitness {ui go_back_screen {{ui current_screen} get back}})
         (fitness_goal {$this change_view fitness FALSE})
         (fitness_goal_set_days_active {$this change_view fitness_goal FALSE})
         (fitness_goal_set_calorie {$this change_view fitness_goal FALSE})
      }
   )
}
{new HamScreen
   fitness_screen
   (panels meta background_panel fitness_panel fitness_info_panel)
   (focus fitness_panel)
   (back main_screen)
   (on_voice_command
      ($command)
      {switch $command
         (screen_name_save_return {helpbar select_left_list_item 1})
         BACRO
         {fitness_panel select_right_list_token $command}
      }
   )
}
{new HamScreen
   goal_reset_warning_screen
   (panels background_confirmation_panel dialog_panel)
   (focus dialog_panel)
   (enter
      {dialog_panel set_custom_ok_no
         fitness_goal_reset_warning
         okay_set_weekly_goals
         no
         1
         FALSE
      }
   )
   (NAV_SELECT_MSG
      {if_else {== $name okay_set_weekly_goals}
         {do
            {fitness_panel change_view fitness_goal TRUE}
            {fitness_panel set allow_voice_control FALSE}
            {calorie_provider clear_saved_highlight}
         }
         {do
            {voice_input_panel activate_voice_context fitness_screen}
         }
      }
      {ui pop_screen}
   )
}