{new HamPanel
   infinite_playlist_panel
   (file "infinite_playlist_menu.milo")
   (focus right_hand.hnl)
   (focus_index 0)
   (back_token back)
   (helpbar_confirm_label help_select)
   (use_gamertag_bg TRUE)
   (enter
      {$this update_all}
      {leaderboards clear_cache}
   )
   (exit {leaderboards clear_cache})
   SELECT_RIGHT_LIST_ITEM
   (poll
      SLIDE_RIGHT_LIST_ITEM
      {if {! {platform_mgr is_ethernet_cable_connected}}
         {ui goto_screen leaderboards_ethernet_disconnected_screen}
      }
   )
   (active_profile_is_signed_into_live
      {if_else {profile_mgr has_active_profile}
         {do
            ($profile {profile_mgr get_active_profile})
            {if_else {platform_mgr is_pad_signed_into_live {$profile get_pad_num}}
               1
               0
            }
         }
         0
      }
   )
   (NAV_SELECT_MSG
      {switch $component
         (right_hand.hnl
            {switch $name
               (infinite_playlist_start_sel
                  {gamemode set_mode infinite_playlist_perform}
                  {song_offer_provider enter}
                  {song_offer_provider setup_quasi_random_songs}
                  {meta_performer start_playlist}
                  {ui goto_screen {gamemode get seldiff_screen}}
               )
               (infinite_playlist_score_sel
                  {print "-----------------  infinite_playlist_score_sel\n"}
                  {leaderboards set_type 4}
                  {ui goto_screen leaderboards_screen}
               )
               (infinite_playlist_endurance_sel
                  {print "-----------------  infinite_playlist_endurance_sel\n"}
                  {leaderboards set_type 5}
                  {ui goto_screen leaderboards_screen}
               )
            }
         )
         (left_hand.hnl
            {switch $name
               (back {ui go_back_screen {{ui current_screen} get back}})
            }
         )
      }
   )
   (NAV_HIGHLIGHT_MSG
      {switch $component
         (right_hand.hnl {set [focus_index] $index})
      }
   )
   (update_all
      {if_else {profile_mgr has_active_profile}
         {do
            ($profile {profile_mgr get_active_profile})
            ($stats {$profile get_stats})
            {has_profile.flow activate}
            {gamertag.lbl set_user_name {$profile get_pad_num}}
            {session_duration.lbl set_token_fmt
               time_text
               {meta_performer get_playlist_elapsed_time_string}
            }
            {set $session_score {player_provider_1 get cumulative_score}}
            {'+=' $session_score {player_provider_2 get cumulative_score}}
            {session_score.lbl set_int $session_score TRUE}
            {set $lifetime_score {$stats get_count kCountStat_InifinitePlaylistScore}}
            {lifetime_score.lbl set_int $lifetime_score TRUE}
            {$profile update_infinite_playlist_time lifetime_duration.lbl}
            {$this set helpbar_confirm_label help_select}
            {helpbar resync}
         }
         {do
            {no_profile.flow activate}
            {helpbar resync}
         }
      }
   )
   (save_load_all_complete_msg {$this update_all})
   (padnum_updated {$this update_all})
   (on_ui_nav_player_change {$this update_all})
   (is_loaded {content_mgr refresh_done})
   (load {content_loading_panel allowed_to_show TRUE})
   (finish_load {content_loading_panel allowed_to_show FALSE})
}
{new HamScreen
   infinite_playlist_screen
   (panels meta background_panel infinite_playlist_panel)
   (focus infinite_playlist_panel)
   (back main_screen)
   (enter)
   (exit)
}