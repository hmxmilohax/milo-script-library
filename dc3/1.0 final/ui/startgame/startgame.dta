{new HamPanel
   startgame_practice_panel
   (file "startgame_practice.milo")
   (focus right_hand.hnl)
   (back_token back)
   (pending_enter FALSE)
   (enter
      {$this set pending_enter FALSE}
      {diff.lbl set
         text_token
         {difficulty_to_sym
            {gamedata getp 0 difficulty}
         }
      }
      {handle ($this set_up)}
      {song_info_panel set_song_list_mode FALSE}
   )
   (exit {$this set pending_enter FALSE})
   (TRANSITION_COMPLETE_MSG
      {if {== [pending_enter] TRUE}
         {$this enter_practice}
      }
   )
   (enter_practice
      {hamprovider set ui_nav_mode loading}
      {if {gamemode in_mode campaign}
         {print "startgame_practice_panel enter_practice : campaign mode\n"}
         {campaign set_campaign_state kCampaignStateBreakItDown}
      }
      {enter_gameplay}
   )
   SELECT_RIGHT_LIST_TOKEN
   (poll SLIDE_RIGHT_LIST_ITEM)
   (NAV_SELECT_MSG
      {switch $name
         (break_it_down_title_song
            {if_else
               {&&
                  {== {player_provider_1 get player_present} TRUE}
                  {== {player_provider_2 get player_present} TRUE}
               }
               {do
                  {startgame_practice_screen set suppress_blacklight_text TRUE}
                  {ui push_screen practice_confirm_screen}
               }
               {$this enter_practice}
            }
         )
         (back {ui go_back_screen {{ui current_screen} get back}})
      }
   )
   (allow_venue_select
      {do
         {if_else {meta_performer is_endgame_song}
            FALSE
            TRUE
         }
      }
   )
}
{new HamScreen
   startgame_practice_screen
   (panels meta background_panel startgame_practice_panel song_info_panel)
   (focus startgame_practice_panel)
   (back {gamemode get seldiff_screen})
   (suppress_blacklight_text FALSE)
   (enter
      {voice_input_panel activate_voice_context practice_startgame_screen}
      {set [suppress_blacklight_text] FALSE}
   )
   (TRANSITION_COMPLETE_MSG
      {set [suppress_blacklight_text] FALSE}
      {startgame_practice_panel transition_complete $new_screen $old_screen}
      {voice_input_panel activate_voice_context practice_startgame_screen}
   )
   (on_voice_command
      ($command)
      {switch $command
         BACRO
         {startgame_practice_panel select_right_list_token $command}
      }
   )
}
{new HamScreen
   startgame_campaign_practice_screen
   (panels meta background_panel startgame_practice_panel song_info_panel)
   (focus startgame_practice_panel)
   (back campaign_choose_mode_screen)
   (enter
      {voice_input_panel activate_voice_context practice_startgame_screen}
   )
   (on_voice_command
      ($command)
      {switch $command
         BACRO
         {startgame_practice_panel select_right_list_token $command}
      }
   )
}
{new HamScreen
   practice_confirm_screen
   (panels background_confirmation_panel dialog_panel)
   (focus dialog_panel)
   (enter {dialog_panel set_ok_no confirm_practice TRUE})
   (NAV_SELECT_MSG
      {switch $name
         (ok {startgame_practice_panel set pending_enter TRUE})
      }
      {ui pop_screen}
   )
}