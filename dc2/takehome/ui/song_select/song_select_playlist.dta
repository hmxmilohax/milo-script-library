{new SongSelectPlaylistPanel
   song_select_playlist_panel
   (file "song_select_playlist.milo")
   (focus right_hand.hnl)
   (focus_song_index 0)
   (top_index 0)
   (back_token back)
   (enter
      {hamprovider add_sink $this (on_ui_nav_player_change) 0 FALSE}
      {player_provider_1 add_sink $this (padnum_updated)}
      {player_provider_2 add_sink $this (padnum_updated)}
      {profile_mgr clear_critical_profile}
      {gamemode set_mode playlist_perform}
      {$this refresh}
      {right_hand.hnl scroll_to_index [focus_song_index] [top_index]}
      {$this display_songs [focus_song_index]}
   )
   (exit
      {hamprovider remove_sink $this on_ui_nav_player_change}
      {player_provider_1 remove_sink $this padnum_updated}
      {player_provider_2 remove_sink $this padnum_updated}
      {set [top_index] {right_hand.hnl get_top_index}}
   )
   (update_all {$this refresh})
   (NAV_SELECT_MSG
      {switch $component
         (right_hand.hnl
            {$this select_playlist}
            {if_else {meta_performer is_playlist_custom}
               {if_else {profile_mgr has_active_profile}
                  {do
                     {if_else {meta_performer is_playlist_playable}
                        {do
                           {profile_mgr set_critical_profile {profile_mgr get_active_profile}}
                           {ui goto_screen song_select_playlist_editplay_screen}
                           skip_select_anim
                        }
                        {do
                           {profile_mgr set_critical_profile {profile_mgr get_active_profile}}
                           {ui goto_screen song_select_playlist_customize_screen}
                        }
                     }
                  }
                  {do
                     {ui push_screen song_select_playlist_need_signin_screen}
                  }
               }
               {do
                  {meta_performer start_playlist}
                  {ui goto_screen {gamemode get seldiff_screen}}
               }
            }
         )
         (left_hand.hnl
            {switch $name
               (back {ui go_back_screen {{ui current_screen} get back}})
            }
         )
      }
   )
   (NAV_HIGHLIGHT_MSG
      {switch $component
         (right_hand.hnl
            {set [focus_song_index] $index}
            {$this display_songs $index}
         )
      }
   )
   (display_songs
      ($index)
      {$this update_songs $index}
   )
   (update_playlist_provider
      ($provider)
      {right_hand.hnl set_provider $provider}
      {right_hand.hnl refresh}
      {right_hand.hnl scroll_to_index [top_index] [top_index]}
   )
   (update_song_provider
      ($provider)
      {songs.lst set_provider $provider}
   )
   (get_selected_playlist_index {right_hand.hnl get_selected})
   (update_songcount
      ($count)
      {if_else {> $count 0}
         {no_songs.lbl set showing FALSE}
         {no_songs.lbl set showing TRUE}
      }
   )
   (update_song_list {songs.lst refresh})
   (padnum_updated {$this update_all})
   (on_ui_nav_player_change {$this update_all})
   (controller_mode_entered {$this update_all})
   (controller_mode_exited {$this update_all})
}
{new HamScreen
   song_select_playlist_screen
   (panels meta background_panel song_select_playlist_panel)
   (focus song_select_playlist_panel)
   (back song_select_screen)
}
{new HamScreen
   song_select_playlist_editplay_screen
   (panels dialog_panel)
   (focus dialog_panel)
   (enter
      {dialog_panel set_custom playlist_editplay playlist_play playlist_edit}
   )
   (NAV_SELECT_MSG
      {switch $name
         (playlist_play
            {meta_performer start_playlist}
            {ui goto_screen {gamemode get seldiff_screen}}
         )
         (playlist_edit {ui goto_screen song_select_playlist_customize_screen})
      }
   )
}
{new HamScreen
   song_select_playlist_need_signin_screen
   (panels dialog_panel)
   (focus dialog_panel)
   (enter {dialog_panel set_ok playlist_need_to_sign_in})
   (NAV_SELECT_MSG {ui pop_screen})
}