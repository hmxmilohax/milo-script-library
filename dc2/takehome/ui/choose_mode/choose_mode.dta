{new ChooseModeProvider choose_mode_provider}
{new HamPanel
   choose_mode_panel
   (file "choose_mode.milo")
   (focus right_hand.hnl)
   (back_token back)
   (enter
      {gamemode set_mode init}
      {choose_mode_provider update_list}
      {right_hand.hnl set_provider choose_mode_provider}
      {song_info_panel set_song_list_mode FALSE}
   )
   (NAV_SELECT_MSG
      {switch $name
         (perform
            {gamemode set_mode perform}
            {ui goto_screen {gamemode get seldiff_screen}}
         )
         (practice
            {gamemode set_mode practice}
            {ui goto_screen {gamemode get seldiff_screen}}
         )
         (dancebattle
            {gamemode set_mode dance_battle}
            {ui goto_screen {gamemode get seldiff_screen}}
         )
         (leaderboards
            {if_else {profile_mgr has_active_profile}
               {do
                  ($profile {profile_mgr get_active_profile})
                  {if_else {platform_mgr is_pad_signed_into_live {$profile get_pad_num}}
                     {do
                        {profile_mgr set_critical_profile $profile}
                        {ui goto_screen leaderboard_select_screen}
                     }
                     {ui goto_screen leaderboards_need_to_sign_in_screen}
                  }
               }
               {ui goto_screen leaderboards_need_to_sign_in_screen}
            }
         )
         (back {ui go_back_screen {{ui current_screen} get back}})
      }
   )
}
{new HamScreen
   choose_mode_screen
   (panels meta background_panel choose_mode_panel song_info_panel)
   (focus choose_mode_panel)
   (back song_select_screen)
   (enter)
}
{new HamScreen
   leaderboards_need_to_sign_in_screen
   (panels meta background_panel dialog_panel)
   (focus dialog_panel)
   (enter {dialog_panel set_ok leaderboards_need_to_sign_in})
   (NAV_SELECT_MSG {ui goto_screen choose_mode_screen})
}