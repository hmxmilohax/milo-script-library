{new MoviePanel
   movie_credits_panel
   (preload FALSE)
   (loop FALSE)
   (audio TRUE)
   (videos win_credits)
   (movie_done
      {if {== {ui current_screen} movie_credits_screen}
         {ui goto_screen text_credits_screen}
      }
   )
}
{new HamScreen
   movie_credits_screen
   (panels meta movie_credits_panel movie_overlay_panel)
   (focus movie_overlay_panel)
   (enter {meta music_stop})
   (skip_selected {ui goto_screen movie_credits_skip_confirm_screen})
}
{new CreditsPanel
   text_credits_panel
   (file "credits.milo")
   (credits_done
      {meta music_start}
      {ui goto_screen {text_credits_screen get next_screen}}
   )
}
{new HamScreen
   movie_credits_skip_confirm_screen
   (panels meta movie_credits_panel dialog_panel)
   (focus dialog_panel)
   (enter {dialog_panel set_yesno credits_confirm_skip})
   (exit)
   (NAV_SELECT_MSG
      {switch $name
         (yes
            {meta music_start}
            {ui goto_screen {text_credits_screen get next_screen}}
         )
         (no {ui goto_screen movie_credits_screen})
      }
   )
}
{new HamScreen
   text_credits_skip_confirm_screen
   (panels meta text_credits_panel dialog_panel)
   (focus dialog_panel)
   (enter {dialog_panel set_yesno credits_confirm_skip})
   (exit)
   (NAV_SELECT_MSG
      {switch $name
         (yes
            {meta music_start}
            {ui goto_screen {text_credits_screen get next_screen}}
         )
         (no
            {text_credits_panel pause_panel FALSE}
            {ui goto_screen text_credits_screen}
         )
      }
   )
}
{new HamScreen
   text_credits_screen
   (panels meta text_credits_panel movie_overlay_panel)
   (focus movie_overlay_panel)
   (scroll_sfx FALSE)
   (is_mesnu_shown FALSE)
   (next_screen main_screen)
   (enter
      {$this handle_unpause}
      {platform_mgr add_sink $this (ui_changed)}
   )
   (exit {platform_mgr remove_sink $this ui_changed})
   (handle_pause
      {$this set is_menu_shown TRUE}
      {text_credits_panel pause_panel TRUE}
   )
   (handle_unpause
      {$this set is_menu_shown FALSE}
      {text_credits_panel pause_panel FALSE}
   )
   (UI_CHANGED_MSG
      {if {&& {platform_mgr guide_showing} {! [is_menu_shown]}}
         {$this handle_pause}
      }
      {if {&& {! {platform_mgr guide_showing}} [is_menu_shown]}
         {$this handle_unpause}
      }
   )
   #ifndef _SHIP
   (BUTTON_DOWN_MSG
      {if_else {text_credits_panel is_cheat_on}
         {text_credits_panel button_down $user $raw_button $action $pad_num}
         kDataUnhandled
      }
   )
   #endif
   (skip_selected
      {if {! {text_credits_panel is_cheat_on}}
         {text_credits_panel pause_panel TRUE}
         {ui goto_screen text_credits_skip_confirm_screen}
      }
   )
}