{new FitnessProvider fitness_provider}
{new HamPanel
   fitness_panel
   (file "fitness.milo")
   (focus right_hand.hnl)
   (focus_index 0)
   (top_index 0)
   (back_token back)
   (helpbar_confirm_label help_select)
   (enter
      {memcardmgr add_sink $this (save_load_all_complete_msg)}
      {hamprovider add_sink $this (on_ui_nav_player_change) 0 FALSE}
      {player_provider_1 add_sink $this (padnum_updated)}
      {player_provider_2 add_sink $this (padnum_updated)}
      {fitness_provider update_list}
      {$this update_all}
      {right_hand.hnl scroll_to_index [focus_index] [top_index]}
   )
   (exit
      {memcardmgr remove_sink $this}
      {hamprovider remove_sink $this on_ui_nav_player_change}
      {player_provider_1 remove_sink $this padnum_updated}
      {player_provider_2 remove_sink $this padnum_updated}
      {set [top_index] {right_hand.hnl get_top_index}}
   )
   (NAV_SELECT_MSG
      {switch $component
         (right_hand.hnl
            {switch $name
               (fitness_option
                  {do
                     ($profile {profile_mgr get_active_profile})
                     {$profile toggle_fitness_mode}
                     {$this update_all}
                     skip_select_anim
                  }
               )
               (fitnessweight_option {ui goto_screen weight_input_screen})
               {do
                  {gamemode set_mode fitness_playlist}
                  {meta_performer set_playlist $name}
                  {meta_performer start_playlist}
                  {ui goto_screen {gamemode get seldiff_screen}}
               }
            }
         )
         (left_hand.hnl
            {switch $name
               (back {ui go_back_screen {{ui current_screen} get back}})
            }
         )
      }
   )
   (NAV_HIGHLIGHT_MSG
      {switch $component
         (right_hand.hnl {set [focus_index] $index})
      }
   )
   (update_all
      {if_else {profile_mgr has_active_profile}
         {do
            ($profile {profile_mgr get_active_profile})
            ($stats {$profile get_stats})
            ($total_time {$stats get_count kCountStat_TotalTimeFitness})
            ($total_calories {$stats get_count kCountStat_TotalCaloriesBurned})
            ($total_songs {$stats get_count kCountStat_TotalFitnessSongs})
            {has_profile.flow activate}
            {right_hand.hnl set_provider fitness_provider}
            {right_hand.hnl refresh}
            {gamertag.lbl set_user_name {$profile get_pad_num}}
            {$profile update_fitness_weight weight.lbl}
            {$profile update_fitness_time time.lbl}
            {calories.lbl set_int {$profile get_fitness_calories} TRUE}
            {$profile update_fitness_total_time total_time.lbl}
            {total_calories.lbl set_int $total_calories TRUE}
            {total_songs.lbl set_int $total_songs TRUE}
            {if_else {> $total_songs 0}
               {average_caloriespersong.lbl set_int
                  {int {/ $total_calories $total_songs}}
                  TRUE
               }
               {average_caloriespersong.lbl set_int 0}
            }
            {$this set helpbar_confirm_label help_select}
            {helpbar resync}
         }
         {do
            {no_profile.flow activate}
            {right_hand.hnl set nav_provider provider_empty.hnp}
            {right_hand.hnl refresh}
            {$this set helpbar_confirm_label none}
            {helpbar resync}
         }
      }
   )
   (save_load_all_complete_msg {$this update_all})
   (padnum_updated {$this update_all})
   (on_ui_nav_player_change {$this update_all})
   (controller_mode_entered {$this update_all})
   (controller_mode_exited {$this update_all})
}
{new HamScreen
   fitness_screen
   (panels meta background_panel fitness_panel)
   (focus fitness_panel)
   (back main_screen)
}