{new LoadingPanel
   loading_panel
   (file [loading_screen_file])
   (next_screen '')
   (loading_screen_file {$this choose_loading_screen})
   (helpbar_allow_controller FALSE)
   (hide_helpbar TRUE)
   (show_hand_help FALSE)
   (load {helpbar sync_to_panel loading_panel})
   (enter
      {if {$this exists show_skip.anim}
         {show_skip.anim set_frame 0}
      }
      {shell_input exit_controller_mode}
      {meta music_stop}
      {meta init_songpreview}
   )
   (block_event
      ($event)
      {!= $event data_error}
   )
}
{new HamScreen
   loading_screen
   (panels loading_panel)
   (enter
      {hamprovider set ui_nav_mode loading}
      {ui goto_screen preloading_screen}
   )
}
#define PRELOAD_PANEL_COMMON (
   (song_mgr song_mgr)
   (current_song {gamedata get song})
   (max_cache_size {'*' 16 {'*' 1024 1024}})
   (preload_files
      ({song_mgr song_file_path {gamedata get song} ".milo"} FALSE)
      ({song_mgr barks_file {gamedata get song}} FALSE)
      ({song_mgr midi_file {gamedata get song}} FALSE)
   )
)
{new PreloadPanel
   preload_panel
   PRELOAD_PANEL_COMMON
   (helpbar_allow_controller FALSE)
   (hide_helpbar TRUE)
   (on_preload_ok {ui goto_screen real_loading_screen})
}
{new HamScreen
   preloading_screen
   (panels loading_panel preload_panel)
   (focus preload_panel)
}
{new HamScreen
   real_loading_screen
   (panels loading_panel)
   (focus loading_panel)
   (enter
      {song_mgr add_recent_song {gamedata get song}}
      {synth stop_all_sfx}
      {ui goto_screen {gamemode get game_screen}}
   )
}