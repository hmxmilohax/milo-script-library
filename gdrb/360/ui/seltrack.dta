{func h2h_set_track_types
   {user_mgr foreach_user $user
      {if {$user is_local}
         {if_else {!= {$user get_controller_type} kControllerGuitar}
            {$user set_track_type {$user get_controller_sym}}
            {$user set_track_type kTrackNone}
         }
      }
   }
}
{func h2h_goto_tracksel_screen
   {h2h_set_track_types}
   {gamecfg send_h2h_set_tracks_msg}
   {ui goto_screen
      {if_else
         {&&
            {gamecfg has_participating_guitar_controller}
            {gamemode get separate_parts}
         }
         h2h_seltrack_screen
         {gamemode get ready_screen}
      }
   }
}
{new UIPanel
   h2h_seltrack_panel
   (file "qp_h2h_seltrack.milo")
   (focus guitar.btn)
   (enter
      {if {== {{session_mgr get_leader_user} get_track_type} kTrackNone}
         {$this set_focus guitar.btn}
      }
      {if_else {== {meta_performer num_songs} 1}
         {song.lbl set_song_name {meta_performer song}}
         {song.lbl set text_token set_list_title}
      }
      {foreach $part (guitar bass)
         {do
            ($button {sprint $part ".btn"})
            ($label {sprint $part ".lbl"})
            {if_else {song_mgr song_part_rank {meta_performer song} $part}
               {do
                  {$this enable $button}
                  {$this enable $label}
               }
               {do
                  {$this disable $button}
                  {$this disable $label}
               }
            }
         }
      }
   )
   (SELECT_MSG
      {do
         ($track {basename {$component name}})
         {user_mgr foreach_user $user_cur
            {if {$user_cur is_participating}
               {if_else {$user_cur is_local}
                  {$user_cur set_track_type $track}
                  {gamecfg set_remote_user_track_type $user_cur $track}
               }
            }
         }
      }
      {ui goto_screen {gamemode get ready_screen}}
   )
}
{new BandScreen
   h2h_seltrack_screen
   (panels meta background_panel postsong_sfx_panel h2h_seltrack_panel)
   (focus h2h_seltrack_panel)
   (background_view seldiff)
   (helpbar
      {if_else {is_leader_local}
         (
            (cancel helpbar_back)
            (confirm helpbar_select)
         )
         ()
      }
   )
   (back song_select_screen)
}