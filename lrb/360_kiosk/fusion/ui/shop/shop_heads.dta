#define HEADS_HANDLERS (
   (file "shop_heads.milo")
   (outfit_char shop_char_panel)
   (outfit_money shop_money_panel)
   (show_outfit_text TRUE)
   (single_screen TRUE)
   (focus 01rockstars.btn)
   (genre "")
   OUTFIT_DEFAULTS
   (enter
      {$this set button_list OUTFIT_GENRES_BUTTONS}
      {$this set current_grp heads.grp}
      {$this set current_list heads.lst}
      {if_else [closet]
         {do
            {heads.grp set_showing FALSE}
            {Categories.grp set_showing TRUE}
            {toggle_buttons [button_list] TRUE}
            {hide_invalid_genres $this heads [closet] FALSE}
         }
         {$this show_genre none}
      }
      OUTFIT_DEFAULT_ENTER
   )
   (show_genre
      ($genre)
      {do
         {toggle_buttons [button_list] FALSE}
         {[current_grp] set_showing TRUE}
         {$this set genre $genre}
         {$this set_focus [current_list]}
         {$this refresh_lists}
         {$this store_outfit}
      }
   )
   (BUTTON_DOWN_MSG
      {switch $action
         (kAction_Confirm
            {switch {$this focus_name}
               (OUTFIT_GENRES_BUTTONS
                  {foreach_int $index 0 {size OUTFIT_GENRES_BUTTONS}
                     {do
                        ($button {elem OUTFIT_GENRES_BUTTONS $index})
                        {if {== {$this focus_name} $button}
                           {$this set current_group heads.grp}
                           {$this set current_btn $button}
                           {$this show_genre {elem OUTFIT_GENRES $index}}
                        }
                     }
                  }
               )
               (heads.lst {$this select_item})
            }
            kDataUnhandled
         )
         (kAction_Cancel
            {if_else [closet]
               {do
                  {switch {$this focus_name}
                     (
                        (heads.lst)
                        {if {!= {$this selected_outfit} ''}
                           {$this undo_outfit}
                        }
                        {toggle_buttons [button_list] TRUE}
                        {$this set_focus [current_btn]}
                        {[current_grp] set_showing FALSE}
                        {$this update}
                     )
                     {do
                        {if_else {== [closet] TRUE}
                           {ui goto_screen shop_mycloset}
                           {ui goto_screen shop_screen}
                        }
                     }
                  }
               }
               {do
                  {$this undo_outfit}
                  {new.grp set_showing FALSE}
                  {ui goto_screen shop_screen}
               }
            }
            {[outfit_char] select_cam "idle"}
            {synth play button_back}
            kDataUnhandled
         )
         (kAction_ViewModify
            {switch {$this focus_name}
               (
                  (heads.lst)
                  {synth play button_toggle}
                  {[outfit_char] select_next_cam}
                  kDataUnhandled
               )
            }
         )
         (kAction_Start
            {if {&& {== {$this focus_name} heads.lst} [can_change_member]}
               {$this set apply_to_all TRUE}
               {$this set current_list heads.lst}
               {$this select_item}
            }
         )
         CHANGE_MEMBER_ACTION
      }
   )
   (setup {$this store_outfit})
)
{new OutfitPanel shop_heads_panel (closet FALSE) HEADS_HANDLERS}
{new OutfitPanel mycloset_heads_panel (closet TRUE) HEADS_HANDLERS}
{new MainScreen
   shop_heads
   (panels
      meta
      hubentourage_panel
      shop_char_panel
      shop_money_panel
      shop_heads_panel
      #ifndef DAN_FAST
      jukebox_panel
      #endif
   )
   (focus shop_heads_panel)
   (helpbar
      (
         (cancel helpbar_back)
         (confirm helpbar_confirm)
      )
   )
}
{new MainScreen
   mycloset_heads
   (panels
      meta
      shop_char_panel
      shop_money_panel
      mycloset_heads_panel
      #ifndef DAN_FAST
      jukebox_panel
      #endif
   )
   (focus mycloset_heads_panel)
   (helpbar
      (
         (cancel helpbar_back)
         (confirm helpbar_confirm)
      )
   )
}