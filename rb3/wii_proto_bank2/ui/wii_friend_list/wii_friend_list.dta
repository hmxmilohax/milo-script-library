{new WiiFriendsDetailsProvider wii_friends_details_provider}
{new UIPanel
   wiifriend_panel
   (file wii_friend_list.milo)
   (enter
      {overshell set_active_status kOvershellWiiFriendInvite}
      {enter.trg trigger}
      {friends.lst set_provider wii_friends_provider}
      {details.lst set_provider wii_friends_details_provider}
      {details.lst auto_scroll}
      {friends.lst set_selected 0}
      {$this set_focus friends.lst}
      {net_sync set_ui_state kNetUI_InvitingFriends}
      {$this check_no_wiifriends}
   )
   (exit
      {exit.trg trigger}
      {overshell set_active_status kOvershellInShell}
   )
   (check_no_wiifriends
      {if_else {<= {friends.lst num_data} 0}
         {do
            {no_wiifriends.trg trigger}
         }
         {do
            {hide_details.trg trigger}
            {show_friendsandprofiles.trg trigger}
            {$this refresh_lists}
         }
      }
   )
   (refresh_lists
      {if {$this is_up}
         {friends.lst refresh}
         {wii_friends_details_provider set_wii_friend {friends.lst selected_pos}}
         {details.lst refresh}
         {$this setup_detail_labels}
      }
   )
   (setup_detail_labels
      {if_else {wii_friends_provider got_server_friend_data}
         {show_details.trg trigger}
         {hide_details.trg trigger}
      }
      {if_else
         {wii_friends_provider is_possessive_suffix_needed
            wii_friends_details_title
         }
         {friends_profiles.lbl set_token_fmt
            wii_friends_details_title
            {wii_friends_details_provider get_friend_name}
            {wii_friends_details_provider get_possessive_suffix}
         }
         {friends_profiles.lbl set_token_fmt
            wii_friends_details_title
            {wii_friends_details_provider get_friend_name}
         }
      }
      {if_else {== {wii_friends_details_provider get_num_profiles} 1}
         {profile_description.lbl set_token_fmt
            wii_friends_details_description_singular
            {wii_friends_details_provider get_friend_name}
            {wii_friends_details_provider get_num_profiles}
         }
         {profile_description.lbl set_token_fmt
            wii_friends_details_description_plural
            {wii_friends_details_provider get_friend_name}
            {wii_friends_details_provider get_num_profiles}
         }
      }
   )
   (BUTTON_DOWN_MSG
      {switch $action
         (kAction_Cancel
            {input_mgr clear_user}
            {wiifriends_screen set exit_screen TRUE}
         )
         (kAction_Confirm
            {if {> {friends.lst num_data} 0}
               {do
                  {wii_friends_provider set_wii_friend {friends.lst selected_pos}}
                  {wiifriend_dialog_screen clear_error}
                  {wiifriend_dialog_screen show}
               }
            }
         )
         kDataUnhandled
      }
      kDataUnhandled
   )
   (SCROLL_MSG
      {if {== $component friends.lst}
         {do
            {wii_friends_details_provider set_wii_friend {friends.lst selected_pos}}
            {$this setup_detail_labels}
            {details.lst set_selected 0}
            {details.lst refresh}
         }
      }
      kDataUnhandled
   )
}
{new BandScreen
   wiifriend_dialog_screen
   (panels dialog_panel)
   (focus dialog_panel)
   (is_error FALSE)
   (error_text "")
   (set_error
      ($text)
      {set [error_text] $text}
      {set [is_error] TRUE}
   )
   (clear_error {set [is_error] FALSE})
   (show
      {if_else {== {ui current_screen} $this}
         {$this configure}
         {unless {== {ui transition_screen} $this}
            {ui push_screen $this}
         }
      }
   )
   (configure_for_invitation
      {cond
         ({wii_friends_provider is_friend_offline}
            {dialog_panel set_ok
               (wii_friends_invite_offline {wii_friends_provider get_friend_name})
               ok.btn
            }
         )
         ({wii_friends_provider is_friend_in_session}
            {dialog_panel set_ok
               (wii_friends_invite_in_session {wii_friends_provider get_friend_name})
               ok.btn
            }
         )
         ({wii_friends_provider is_invitation_pending_from_friend}
            {if_else
               {wii_friends_provider is_possessive_suffix_needed
                  wii_friends_invite_already_received
               }
               {dialog_panel set_ok
                  (wii_friends_invite_already_received
                     {wii_friends_provider get_friend_name}
                     {wii_friends_provider get_friend_name}
                     {wii_friends_provider get_possessive_suffix}
                  )
                  ok.btn
               }
               {dialog_panel set_ok
                  (wii_friends_invite_already_received
                     {wii_friends_provider get_friend_name}
                     {wii_friends_provider get_friend_name}
                  )
                  ok.btn
               }
            }
         )
         ({wii_friends_provider is_friend_already_invited}
            {dialog_panel set_ok (wii_friends_invite_already_sent) ok.btn}
         )
         {dialog_panel set_yesno
            (wii_friends_send_invitation {wii_friends_provider get_friend_name})
            no.btn
         }
      }
   )
   (configure_for_error {dialog_panel set_ok [error_text] ok.btn})
   (configure
      {if_else {$this get is_error}
         {$this configure_for_error}
         {$this configure_for_invitation}
      }
   )
   (enter {$this configure})
   (SELECT_MSG
      {if_else {$this get is_error}
         {wiifriends_screen set exit_screen TRUE}
         {do
            {if {== $component yes.btn}
               {wii_friends_provider invite_friend}
            }
            {ui pop_screen}
         }
      }
   )
}
{new WiiFriendsScreen
   wiifriends_screen
   (prevents_override TRUE)
   (exit_screen FALSE)
   (exiting_screen FALSE)
   (handling_session_disconnect FALSE)
   (handling_nwc24_failure FALSE)
   (panels meta sv3_panel wiifriend_panel)
   (focus wiifriend_panel)
   (enter
      {$this set exit_screen FALSE}
      {$this set exiting_screen FALSE}
      {$this set handling_session_disconnect FALSE}
      {set [handling_nwc24_failure] FALSE}
   )
   (exit)
   (poll
      {if {&& {! {$this get exiting_screen}} {$this get_user}}
         {if_else {$this get exit_screen}
            {do
               {$this set exiting_screen TRUE}
               {$this return_to_main_menu}
            }
            {if_else {platform_mgr is_user_signed_in {$this get_user}}
               {if_else {wii_friend_mgr has_nwc24_lib_failed}
                  {unless [handling_nwc24_failure]
                     {do
                        {wiifriend_dialog_screen set_error {wii_friend_mgr get_nwc24_error_text}}
                        {wiifriend_dialog_screen show}
                        {if
                           {'||'
                              {== {ui current_screen} wiifriend_dialog_screen}
                              {== {ui transition_screen} wiifriend_dialog_screen}
                           }
                           {set [wiifriend_dialog_screen] TRUE}
                        }
                     }
                  }
                  {unless {session_mgr is_online_enabled}
                     {unless {$this get handling_session_disconnect}
                        {unless
                           {'||'
                              {== {ui current_screen} wiifriend_dialog_screen}
                              {== {ui transition_screen} wiifriend_dialog_screen}
                           }
                           {do
                              {wiifriend_dialog_screen set_error wii_friends_session_ended}
                              {wiifriend_dialog_screen show}
                              {if
                                 {'||'
                                    {== {ui current_screen} wiifriend_dialog_screen}
                                    {== {ui transition_screen} wiifriend_dialog_screen}
                                 }
                                 {$this set handling_session_disconnect TRUE}
                              }
                           }
                        }
                     }
                  }
               }
               {$this set exit_screen TRUE}
            }
         }
      }
   )
   (exit)
}
{new BandScreen
   wii_friends_prompt
   (panels dialog_panel)
   (focus dialog_panel)
   (show_followup FALSE)
   (enter
      {profile_mgr set_wii_friends_prompt_shown}
      {dialog_panel set_yesno wii_friends_prompt_text no.btn}
      {net_sync set_ui_state kNetUI_InvitingFriends}
      {set [show_followup] FALSE}
   )
   (exit)
   (poll
      {if [show_followup]
         {if_else {profile_mgr get_using_wii_friends}
            {dialog_panel set_ok wii_friends_prompt_followup_yes ok.btn}
            {dialog_panel set_ok wii_friends_prompt_followup_no ok.btn}
         }
      }
   )
   (SELECT_MSG
      {switch $component
         (yes.btn
            {profile_mgr set_using_wii_friends 1}
            {set [show_followup] 1}
         )
         (no.btn
            {profile_mgr set_using_wii_friends 0}
            {set [show_followup] 1}
         )
         (ok.btn {wiifriends_screen return_to_main_menu})
         kDataUnhandled
      }
   )
}